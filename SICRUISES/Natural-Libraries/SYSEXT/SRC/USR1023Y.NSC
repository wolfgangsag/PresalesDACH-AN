* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
/*  This copycode can directly be used. For more information read the
/*  text object 'USR1023T' in library SYSEXT.
/***********************************************************************
/*
/*  Copycode Interface .... USR1023Y    /*  Copycode source
/*  Documentation ......... USR1023T    /*  Text object
/*  Example Program ....... USR1023P    /*  Source program
/*
/*  Function .............. Convert a store clock into microseconds.
/*
/*
/*  Transferred Parameters
/*  ----------------------
/*  &1&   Input:  store clock value, format (B8), such as *TIMESTMP
/*  &2&   Output: microseconds since 1900-01-01 00:00:00,
/*                format (P19), (N19) or (F8)
/*
/*
/*  Additional Data Definitions
/*  ---------------------------
/*  1 USR1023_B8     (B8)  /* variables used in copycode USR1023Y
/*  1 REDEFINE USR1023_B8
/*    2 USR1023_B81  (B3)
/*    2 USR1023_B82  (B3)
/*    2 USR1023_B83A (B1)
/*    2 USR1023_B83B (B1)
/*  1 USR1023_I41    (I4)
/*  1 USR1023_I42    (I4)
/*  1 USR1023_I43    (I4)
/*
/*
/*  Using Copycode Interface
/*  ------------------------
/*  Copy the copycode USR1023Y to the required library.
/*
/*  Add the additional data definitions for the copycode to the data
/*  definitions of your program.
/*
/*  Use the copycode with the INCLUDE statement:
/*     INCLUDE USR1023Y 'C-STORECLOCK' 'C-MICROSECONDS'
***********************************************************************
*
* Get binary value (B8)
*
MOVE &1& TO USR1023_B8
*
* Transform data into integer
*
USR1023_I41 := USR1023_B81
USR1023_I42 := USR1023_B82
USR1023_I43 := USR1023_B83A
*
* Convert into microseconds (possible format: (P19), (N19), (F8))
*
&2& := ( USR1023_I41 * 0000000000268435456 ) +
       ( USR1023_I42 * 16 ) +
       ( USR1023_I43 / 16 )
*
***********************************************************************
