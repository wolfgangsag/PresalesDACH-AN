* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* TODO Enter your code here
DEFINE DATA 
PARAMETER USING NCCOMM-P
PARAMETER USING NCCOUP-P 
*
LOCAL USING NCDATA-L
LOCAL
1 ID-CUSTOMER-NEW-N (N8)
1 ID-CRUISE-NEW-N (N8)
1 CHECK-DATE (A8)
1 REDEFINE CHECK-DATE
  2 CHECK-YEAR-N (N4)
  2 CHECK-MON-N  (A2)
  2 CHECK-DAY-N  (A2)
1 REDEFINE CHECK-DATE
  2 CHECK-DATE-N (N8)
*
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 V-SERVICENAME (A25) CONST<'Ã„ndern Buchungsdaten'>
*
END-DEFINE
*
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME'
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
ON ERROR
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  BACKOUT TRANSACTION
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
*
IF P-SELECTION.P-CONTRACT-ID = 0
  MOVE 9921 TO MSG-GROUP-PARA.MSG-NR
ELSE
  F1.
 FIND NCCONTRACT WITH NCCONTRACT.CONTRACT-ID = P-SELECTION.P-CONTRACT-ID
    IF NO RECORD FOUND
      MOVE 9922 TO  MSG-GROUP-PARA.MSG-NR
      ESCAPE BOTTOM (F1.)
    END-NOREC
    PERFORM HANDLE-INPUT-DATA
    IF MSG-GROUP-PARA.MSG-NR = 9801
      UPDATE
      END TRANSACTION
    ELSE
      BACKOUT TRANSACTION
    END-IF
    ESCAPE BOTTOM (F1.)
  END-FIND
END-IF
*
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
DEFINE SUBROUTINE HANDLE-INPUT-DATA
*
MOVE 9801 TO MSG-GROUP-PARA.MSG-NR
*
DECIDE FOR EVERY CONDITION
*
  WHEN WEEK-COUNT NE  ' '
     AND  WEEK-COUNT NE '1' AND WEEK-COUNT NE '2' AND  WEEK-COUNT NE '3'
    MOVE 9915 TO  MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
*
  WHEN ID-CUSTOMER-NEW NE  ' '  AND NOT ID-CUSTOMER-NEW IS (N8)
    MOVE 9919 TO MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
  WHEN ID-CUSTOMER-NEW IS (N8)  
    COMPUTE ID-CUSTOMER-NEW-N  = VAL(ID-CUSTOMER-NEW)
    FIND NCCUSTOMER PERSON-ID = ID-CUSTOMER-NEW-N
      IF NO RECORDS FOUND
        MOVE 9918 TO MSG-GROUP-PARA.MSG-NR
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
    MOVE ID-CUSTOMER-NEW-N TO NCCONTRACT.ID-CUSTOMER
*
  WHEN ID-CRUISE-NEW NE  ' '  AND NOT ID-CRUISE-NEW IS (N8)
    MOVE 9917 TO MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
  WHEN ID-CRUISE-NEW IS (N8)  
    COMPUTE ID-CRUISE-NEW-N  = VAL(ID-CRUISE-NEW)
    FIND NCCRUISE CRUISE-ID = ID-CRUISE-NEW-N
      IF NO RECORDS FOUND
        MOVE 9916 TO MSG-GROUP-PARA.MSG-NR
        ESCAPE ROUTINE
      END-NOREC
      MOVE ID-CRUISE-NEW-N TO NCCONTRACT.ID-CRUISE
    END-FIND
*
  WHEN DATE-RESERVATION-NEW NE ' '
    IF DATE-RESERVATION-NEW NE MASK (YYYYMMDD)
      MOVE 9914 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE DATE-RESERVATION-NEW TO CHECK-DATE
    IF CHECK-YEAR-N LT 2015 OR CHECK-YEAR-N GT 2020
      MOVE 9913 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE CHECK-DATE-N TO NCCONTRACT.DATE-RESERVATION

*
  WHEN DATE-BOOKING-NEW NE ' '
    IF DATE-BOOKING-NEW NE MASK (YYYYMMDD)
      MOVE 9912 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE DATE-BOOKING-NEW TO CHECK-DATE
    IF CHECK-YEAR-N LT 2015 OR CHECK-YEAR-N GT 2020
      MOVE 9911 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE CHECK-DATE-N TO NCCONTRACT.DATE-BOOKING 
  WHEN NONE IGNORE
END-DECIDE
*
IF WEEK-COUNT NE  ' '
  FIND NCCRUISE CRUISE-ID = NCCONTRACT.ID-CRUISE
    DECIDE ON FIRST VALUE OF WEEK-COUNT
      VALUE '1'
        MOVE NCCRUISE.PRICE-1W TO NCCONTRACT.PRICE
      VALUE '2'
        MOVE NCCRUISE.PRICE-2W TO NCCONTRACT.PRICE
      VALUE '3'
        MOVE NCCRUISE.PRICE-3W TO NCCONTRACT.PRICE
      NONE 
        MOVE 9915 TO MSG-GROUP-PARA.MSG-NR
        ESCAPE ROUTINE
    END-DECIDE
  END-FIND
END-IF
*
END-SUBROUTINE
* ------------------------------------------------------------------
INCLUDE UPDLOG-I 'V-SERVICENAME'  
* ------------------------------------------------------------------
*
END
