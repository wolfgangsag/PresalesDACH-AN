* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* TODO Enter your code here
DEFINE DATA 
PARAMETER USING NCCOMM-P /* Comm Parameters
PARAMETER USING NCCONL-P /* Contract List
PARAMETER 
1 P-SELCUSTOMERID (N8)
*
LOCAL USING NCDATA-L
LOCAL
1 C-RECCNT (P10)
1 T-START-DATE (A10)
1 T-END-DATE (A10)
*
1 V-SERVICENAME (A20) CONST<'Kunde/Buchungsliste'>
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
END-DEFINE
*
* ------------------------------------------------------------------
ON ERROR
  REDUCE ARRAY P-CONTRACT-DATA TO  0
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  ESCAPE ROUTINE 
END-ERROR
* ------------------------------------------------------------------
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME'
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
READ NCCONTRACT DESCENDING BY CONTRACT-ID  
  IF NCCONTRACT.ID-CUSTOMER = P-SELCUSTOMERID
*
    ADD 1 TO C-RECCNT
    EXPAND ARRAY P-CONTRACT-DATA TO (1:C-RECCNT)
*
    MOVE EDITED  NCCONTRACT.PRICE (EM=ZZZZZZZZZ9.99) TO
      P-CONTRACT-DATA.PRICE(C-RECCNT)
    MOVE EDITED  NCCONTRACT.DATE-CANCELLATION (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-CANCELLATION(C-RECCNT)
    MOVE EDITED  NCCONTRACT.DATE-RESERVATION (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-RESERVATION(C-RECCNT)
    MOVE EDITED  NCCONTRACT.DATE-BOOKING     (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-BOOKING (C-RECCNT)
* 
    COMPRESS  NCCONTRACT.CONTRACT-ID                    INTO
      P-CONTRACT-DATA.CONTRACT-ID(C-RECCNT)
    COMPRESS P-CONTRACT-DATA.PRICE(C-RECCNT)            INTO
      P-CONTRACT-DATA.PRICE(C-RECCNT)
    COMPRESS P-CONTRACT-DATA.DATE-RESERVATION(C-RECCNT) INTO
      P-CONTRACT-DATA.DATE-RESERVATION(C-RECCNT)
    COMPRESS P-CONTRACT-DATA.DATE-BOOKING(C-RECCNT)     INTO
      P-CONTRACT-DATA.DATE-BOOKING(C-RECCNT)
*
    F1.
    FIND  NCCUSTOMER PERSON-ID = NCCONTRACT.ID-CUSTOMER
      COMPRESS NCCUSTOMER.SURNAME  ',' NCCUSTOMER.FIRST-NAME-1
        INTO P-CONTRACT-DATA.NAME-CUSTOMER(C-RECCNT) LEAVING NO
      ESCAPE BOTTOM (F1.)
    END-FIND
*
    F2.
    FIND  NCCRUISE CRUISE-ID = NCCONTRACT.ID-CRUISE
      COMPRESS NCCRUISE.START-HARBOR '-' NCCRUISE.DESTINATION-HARBOR
        INTO P-CONTRACT-DATA.ROUTE-CRUISE(C-RECCNT)
*
      MOVE EDITED  NCCRUISE.START-DATE (EM=9999'-'99'-'99)
        TO T-START-DATE
      MOVE EDITED  NCCRUISE.END-DATE   (EM=9999'-'99'-'99)
        TO T-END-DATE
      COMPRESS T-START-DATE '-' T-END-DATE
        INTO P-CONTRACT-DATA.DATE-CRUISE(C-RECCNT)
*
      F3.
      FIND NCYACHT NCYACHT.YACHT-ID = NCCRUISE.ID-YACHT
        COMPRESS NCYACHT.YACHT-NAME INTO
          P-CONTRACT-DATA.NAME-YACHT(C-RECCNT)
        ESCAPE BOTTOM (F3.)
      END-FIND
      ESCAPE BOTTOM (F2.)
    END-FIND
*
  END-IF
END-READ
*
DECIDE FOR FIRST CONDITION
  WHEN C-RECCNT = 0
    MOVE 9856 TO MSG-GROUP-PARA.MSG-NR
  WHEN NONE
    MOVE 9806 TO MSG-GROUP-PARA.MSG-NR
END-DECIDE
* ------------------------------------------------------------------
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
INCLUDE ENLLOG-I 'V-SERVICENAME'
* ------------------------------------------------------------------
*
END
