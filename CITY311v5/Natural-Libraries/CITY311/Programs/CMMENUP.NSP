* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Main program generated for
/** adapter <:link CITY311/CIADMINA>.
/*
/** <h1>Program CMMENUP.</h1>
/** <p>Program der Mobi311 Anwendung</p>
/** <p>New important statements:</p>
/**     <ul type="square">
/**         <li>1. CAMENUP für mobile Services</li>
/**     </ul>
/** <p>Historie Änderungen</p>
/**     <ul type="square">
/**         <li>1.</li>
/**     </ul>
/** :author rfa
/** :version Generated by Natural for Ajax.
/*
DEFINE DATA GLOBAL
  USING CCGLOBG
LOCAL
/*( PARAMETER
1 BUTTONSTART (A) DYNAMIC
1 CAP
2 DATUM (A) DYNAMIC
2 MESSAGE (A) DYNAMIC
2 MYLOGINIMAGE (A) DYNAMIC
2 PANELVISI_COMMENT (L)
2 PANELVISI_INCDETAILS (L)
2 PANELVISI_INCLIST (L)
2 PANELVISI_INCPICS (L)
2 PANELVISI_LOGIN (L)
1 CAP_DETPANELNAME (A) DYNAMIC
1 CAP_INCCOMMENTPANELNAME (A) DYNAMIC
1 CAP_INCLISTPANELNAME (A) DYNAMIC
1 CAP_PICPANELNAME (A) DYNAMIC
1 CAPL_SELECTED
2 CAPLVA1 (A) DYNAMIC
2 CAPLVA2 (A) DYNAMIC
2 CAPLVA3 (A) DYNAMIC
2 CAPLVA4 (A) DYNAMIC
2 CAPLVA5 (A) DYNAMIC
1 CAPLINES (1:*)
2 CAPLVA1 (A) DYNAMIC
2 CAPLVA2 (A) DYNAMIC
2 CAPLVA3 (A) DYNAMIC
2 CAPLVA4 (A) DYNAMIC
2 CAPLVA5 (A) DYNAMIC
1 INC
2 STARTORG (A) DYNAMIC
2 STARTTOWN (A) DYNAMIC
1 INCCOMMENT
2 BACK2DBUTTON (A) DYNAMIC
2 BACK2LBUTTON (A) DYNAMIC
2 COMLABEL (A) DYNAMIC
2 COMVALUE (A) DYNAMIC
2 ENDDATELABEL (A) DYNAMIC
2 ENDDATEVALUE (A) DYNAMIC
2 INCDATELABEL (A) DYNAMIC
2 INCDATEVALUE (A) DYNAMIC
2 INCTYPELABEL (A) DYNAMIC
2 INCTYPEVALUE (A) DYNAMIC
2 MESSAGE (A) DYNAMIC
2 NEWPRIOCBOXV1 (L)
2 NEWPRIOCBOXV2 (L)
2 NEWPRIOCBOXV3 (L)
2 NEWSTATCBOXV1 (L)
2 NEWSTATCBOXV2 (L)
2 OPENBUTTON (A) DYNAMIC
2 SAVEBUTTON (A) DYNAMIC
2 TEXTNEWPRIO (A) DYNAMIC
2 TEXTNEWSTATUS (A) DYNAMIC
1 INCDET
2 LABEL1 (A) DYNAMIC
2 LABEL10 (A) DYNAMIC
2 LABEL2 (A) DYNAMIC
2 LABEL3 (A) DYNAMIC
2 LABEL4 (A) DYNAMIC
2 LABEL5 (A) DYNAMIC
2 LABEL6 (A) DYNAMIC
2 LABEL7 (A) DYNAMIC
2 LABEL8 (A) DYNAMIC
2 LABEL9 (A) DYNAMIC
2 VALUE1 (A) DYNAMIC
2 VALUE10 (A) DYNAMIC
2 VALUE2 (A) DYNAMIC
2 VALUE3 (A) DYNAMIC
2 VALUE4 (A) DYNAMIC
2 VALUE5 (A) DYNAMIC
2 VALUE6 (A) DYNAMIC
2 VALUE7 (A) DYNAMIC
2 VALUE8 (A) DYNAMIC
2 VALUE9 (A) DYNAMIC
2 VALUEPIC1 (A) DYNAMIC
1 OSADRESS (A) DYNAMIC
1 OSLATITUDE (A) DYNAMIC
1 OSLONGITUDE (A) DYNAMIC
1 OSSELECTED
2 ADDRESS (A) DYNAMIC
2 LATITUDE (A) DYNAMIC
2 LONGITUDE (A) DYNAMIC
1 XCIOBJECTS (1:*)
2 CONTENT (B) DYNAMIC
2 CONTENTID (A) DYNAMIC
2 CONTENTTYPE (A) DYNAMIC
/*) END-PARAMETER
LOCAL
    USING CCVIEWSL
/* =======================================================================
LOCAL
1 L-RI (N2)
1 V-BLOB-B    (B) DYNAMIC
1 V-BLOB-A  (A10)
1 V-URL       (A) DYNAMIC
1 V-SUBERRFOUND    (L)  INIT<FALSE>
1 V-INCHISTC       (I2)
1 V-STACKMESSAGE (A60)
1 V-TOPMESSAGE  (A/1:10) DYNAMIC
1 V-ERRTST (N1)
1 V-DATA (N3)
END-DEFINE
/* =======================================================================
ON ERROR
  COMPRESS V-TOPMESSAGE (9) "(" *ERROR-NR ")" INTO V-STACKMESSAGE 
  RELEASE STACK
  STACK TOP DATA V-STACKMESSAGE
  STACK TOP COMMAND "CMMENUP"
  STOP
END-ERROR
/* =======================================================================
MOVE *DATA TO V-DATA
IF V-DATA NE -1 AND V-DATA NE 0
  INPUT (AD=I) V-STACKMESSAGE (AL=60)
END-IF
/* =======================================================================
/* =======================================================================
CCG-LOGINSTATUS := 0
/* =======================================================================
/*
MOVE 2 TO CCG-LANG *LANGUAGE
MOVE *DATV  TO CAP.DATUM
PERFORM SET-LLSTRINGS
/*
IF V-STACKMESSAGE NE " "
   COMPRESS V-STACKMESSAGE   INTO CAP.MESSAGE
ELSE
   COMPRESS V-TOPMESSAGE (8) INTO CAP.MESSAGE
END-IF
/*
/* =======================================================================
PERFORM MAKE-PANELSUNVISIBLEE
PANELVISI_LOGIN        :=  FALSE /*TRUE mod ANAK
CAP.MYLOGINIMAGE       := "./images/city311-2.png"
/* =======================================================================
PROCESS PAGE USING "CMMENUA"
/* =======================================================================
RESET INCCOMMENT.MESSAGE  CAP.MESSAGE
/* COMPUTE V-ERRTST = V-ERRTST / V-ERRTST   /* 4 error test
/* =======================================================================
/* INPUT (AD=O) *PAGE-EVENT (AL=20)
DECIDE ON FIRST *PAGE-EVENT
/* =======================================
  VALUE U'nat:page.end', U'nat:browser.end', U'onLogout'
    /* Page closed.
    TERMINATE
/* =======================================
  VALUE U'onSetge'
    /* TODO: Implement event code.
    PERFORM SET-GE
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onSeten'
    /* TODO: Implement event code.
    PERFORM SET-EN
    PROCESS PAGE UPDATE FULL
* =======================================
  VALUE U'onSetsp'
    /* TODO: Implement event code.
    PERFORM SET-SP
    PROCESS PAGE UPDATE FULL
* =======================================
  VALUE U'onSetpo'
    /* TODO: Implement event code.
    PERFORM SET-PO
    PROCESS PAGE UPDATE FULL

/* =======================================
  VALUE U'onLoginbutton' ,  U'onIncBack2l'
    /* TODO: Implement event code.
/* =======================================================================
    FETCH RETURN 'CMINIGLP'
/* =======================================================================
    PERFORM MAKE-PANELSUNVISIBLEE
    PERFORM SET-LOGIN
    PERFORM FILL-INCGRID
    PANELVISI_INCDETAILS  :=  FALSE
    PANELVISI_INCLIST     :=  TRUE
    PANELVISI_LOGIN       :=  FALSE
    PANELVISI_COMMENT     :=  FALSE
    PANELVISI_INCPICS     :=  FALSE
    MOVE "Welcome" TO CAP.MESSAGE
    CCG-LOGINSTATUS := 1
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onLogin'
    /* TODO: Implement event code.

    PERFORM SET-LOGIN
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCDETAILS  :=  FALSE
      PANELVISI_INCLIST     :=  TRUE
      PANELVISI_INCPICS     :=  FALSE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_COMMENT     :=  FALSE
      CCG-LOGINSTATUS := 0
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onInclist', U'onIncBack2l'
    /* TODO: Implement event code.
    PERFORM FILL-INCGRID
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCDETAILS  :=  FALSE
      PANELVISI_INCLIST     :=  TRUE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_COMMENT     :=  FALSE
      PANELVISI_INCPICS     :=  FALSE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onIncdetails',  U'onIncBack2d'
    /* TODO: Implement event code.
    PERFORM FILL-INCDETAILS
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCDETAILS  :=  TRUE
      PANELVISI_INCPICS     :=  TRUE
      PANELVISI_INCLIST     :=  FALSE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_COMMENT     :=  FALSE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onIncComment'
    /* TODO: Implement event code.
    PERFORM FILL-INCCOMMENT
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE    
      PANELVISI_COMMENT     :=  TRUE
      PANELVISI_INCDETAILS  :=  FALSE
      PANELVISI_INCLIST     :=  FALSE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_INCPICS     :=  FALSE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onLogout'
    /* TODO: Implement event code.
    REDUCE ARRAY CAPMENU TO 0
    RESET CAPMENU(*)
    PERFORM MAKE-PANELSUNVISIBLEE
    PERFORM SET-LOGOUT
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onCitizenIncs'
    /* TODO: Implement event code.

    PERFORM FILL-INCGRID
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCLIST       :=  TRUE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'caplines.onInlineClick'
    /* TODO: Implement event code.

    PERFORM FILL-INCDETAILS
    IF V-SUBERRFOUND = FALSE
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCDETAILS  :=  TRUE
      PANELVISI_INCPICS     :=  TRUE
      PANELVISI_INCLIST     :=  FALSE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_COMMENT     :=  FALSE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onSaveInccomment'
    /* TODO: Implement event code.
    PERFORM SAVE-INCCOMMENT
    IF V-SUBERRFOUND = FALSE
      PERFORM FILL-INCDETAILS
      PERFORM MAKE-PANELSUNVISIBLEE
      PANELVISI_INCPICS     :=  TRUE
      PANELVISI_INCDETAILS  :=  TRUE
      PANELVISI_INCLIST     :=  FALSE
      PANELVISI_LOGIN       :=  FALSE
      PANELVISI_COMMENT     :=  FALSE
    END-IF
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onCommnewprio1'
    MOVE TRUE             TO  INCCOMMENT.NEWPRIOCBOXV1
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV2
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV3
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onCommnewprio2'
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV1
    MOVE TRUE             TO  INCCOMMENT.NEWPRIOCBOXV2
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV3
    PROCESS PAGE UPDATE FULL
    /* =======================================
  VALUE U'onCommnewprio3'
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV2
    MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV1
    MOVE TRUE             TO  INCCOMMENT.NEWPRIOCBOXV3
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onCommnewstat1'
    /* TODO: Implement event code.
    MOVE TRUE             TO  INCCOMMENT.NEWSTATCBOXV1
    MOVE FALSE            TO  INCCOMMENT.NEWSTATCBOXV2
    PROCESS PAGE UPDATE FULL
/* =======================================
  VALUE U'onCommnewstat2'
    /* TODO: Implement event code.
    MOVE TRUE             TO  INCCOMMENT.NEWSTATCBOXV2
    MOVE FALSE            TO  INCCOMMENT.NEWSTATCBOXV1
    PROCESS PAGE UPDATE FULL
/* =======================================
  NONE VALUE
    /* Unhandled events.
    PROCESS PAGE UPDATE
/* =======================================
END-DECIDE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE MAKE-PANELSUNVISIBLEE
/*
PANELVISI_INCDETAILS  :=  FALSE
PANELVISI_INCLIST     :=  FALSE
PANELVISI_LOGIN       :=  FALSE
PANELVISI_COMMENT     :=  FALSE
PANELVISI_INCPICS     :=  FALSE
/*
END-SUBROUTINE
/* =======================================================================
DEFINE SUBROUTINE FILL-PAGESTRINGS
/*
IGNORE
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE FILL-INCGRID
/*
V-SUBERRFOUND := TRUE
/*
REDUCE ARRAY CAPLINES TO  0
/*
COMPUTE L-RI  = 0
/*
READ (30) INCV DESCENDING  BY INC-ID STARTING FROM 'Y' 
/*
  ADD 1 TO L-RI
  EXPAND ARRAY CAPLINES TO (1:L-RI) 
/*
/* INPUT  (AD=O) INCV.INC-TYPE (AL=10) 
  MOVE INCV.INC-PRIO                  TO  CAPLINES.CAPLVA3(L-RI)
  MOVE INCV.INC-TYPE                  TO  CAPLINES.CAPLVA1(L-RI)
  MOVE INCV.INC-STAT                  TO  CAPLINES.CAPLVA4(L-RI)
  MOVE SUBSTR(INCV.INC-OPENED,1,16)   TO  CAPLINES.CAPLVA2(L-RI)
  MOVE  INCV.INC-ID                   TO  CAPLINES.CAPLVA5(L-RI)   
/*
END-READ
/*
V-SUBERRFOUND := FALSE
END-SUBROUTINE
/* =======================================================================
DEFINE SUBROUTINE SAVE-INCCOMMENT
/*
V-SUBERRFOUND := TRUE
/*
IF CCG-INCSELECTEDID = " "
  COMPRESS V-TOPMESSAGE (1)  INTO CAP.MESSAGE
  ESCAPE ROUTINE
END-IF
/*
IF INCCOMMENT.COMVALUE = " "
  COMPRESS V-TOPMESSAGE (2) TO INCCOMMENT.MESSAGE
  ESCAPE ROUTINE
END-IF
/*
FIND INCV INCV.INC-ID = CCG-INCSELECTEDID
  IF NO
    COMPRESS V-TOPMESSAGE (3) TO CAP.MESSAGE
    ESCAPE ROUTINE
  END-NOREC
  FOR V-INCHISTC 1 TO C*INC-HISTORY
    IF INCV.HIS-DAT (V-INCHISTC) = " "
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
/*
FIND INCV INCV.INC-ID = CCG-INCSELECTEDID
/*
  DECIDE FOR FIRST CONDITION
    WHEN INCCOMMENT.NEWPRIOCBOXV1 = TRUE
      MOVE "low"                 TO INCV.INC-PRIO
    WHEN INCCOMMENT.NEWPRIOCBOXV2 = TRUE
      MOVE "medium"              TO INCV.INC-PRIO
    WHEN INCCOMMENT.NEWPRIOCBOXV3 = TRUE
      MOVE "high"                TO INCV.INC-PRIO
    WHEN NONE IGNORE
  END-DECIDE
/*
/* INPUT (AD=O) 'p' INCV.INC-PRIO (AL=10) 's' INCV.INC-STAT (AL=10)
/*
  DECIDE FOR FIRST CONDITION
    WHEN INCCOMMENT.NEWSTATCBOXV1 = TRUE
      MOVE "active"              TO INCV.INC-STAT
    WHEN INCCOMMENT.NEWSTATCBOXV2 = TRUE
      MOVE "solved"              TO INCV.INC-STAT
    WHEN NONE IGNORE
  END-DECIDE
/*
  MOVE INCV.INC-STAT           TO   INCV.HIS-STAT        (V-INCHISTC)
  MOVE INCV.INC-PRIO           TO   INCV.HIS-PRIO        (V-INCHISTC)
  MOVE INCCOMMENT.COMVALUE     TO   INCV.HIS-DESC        (V-INCHISTC)
/*
  COMPRESS INC.STARTORG      INTO   INCV.HIS-AUTHOR      (V-INCHISTC)
  COMPRESS *DAT4I '&' *TIME       
    INTO INCV.HIS-DAT    (V-INCHISTC)  LEAVING NO
  MOVE SUBSTR(INCV.HIS-DAT(V-INCHISTC) ,1,16)  
    TO INCV.HIS-DAT  (V-INCHISTC)
  EXAMINE INCV.HIS-DAT (V-INCHISTC)     FOR '.' REPLACE WITH ' '
  EXAMINE INCV.HIS-DAT (V-INCHISTC)     FOR '&' REPLACE WITH ' '
  /*
  UPDATE
  END TRANSACTION
END-FIND
/*
COMPRESS V-TOPMESSAGE (4) TO CAP.MESSAGE
V-SUBERRFOUND := FALSE
/*
END-SUBROUTINE
/* =======================================================================
DEFINE SUBROUTINE FILL-INCCOMMENT
/*
V-SUBERRFOUND := TRUE
/*
IF CCG-INCSELECTEDID = " "
  COMPRESS V-TOPMESSAGE (1) TO CAP.MESSAGE
  ESCAPE ROUTINE
END-IF
FIND INCV INCV.INC-ID = CCG-INCSELECTEDID
  IF NO 
    COMPRESS V-TOPMESSAGE (2) INTO CAP.MESSAGE
    ESCAPE ROUTINE
  END-NOREC
  MOVE SUBSTR(INCV.INC-OPENED,1,16) TO INCCOMMENT.INCDATEVALUE
  MOVE INCV.INC-TYPE                TO INCCOMMENT.INCTYPEVALUE
END-FIND
* /*
* MOVE "My Comment"      TO  INCCOMMENT.COMLABEL
* MOVE "Save Comment"    TO  INCCOMMENT.SAVEBUTTON
* MOVE "My Comment"      TO  INCCOMMENT.OPENBUTTON
* MOVE "New Priority"    TO INCCOMMENT.TEXTNEWPRIO
* MOVE "New Status"      TO INCCOMMENT.TEXTNEWSTATUS
/*
MOVE TRUE             TO  INCCOMMENT.NEWSTATCBOXV1
MOVE FALSE            TO  INCCOMMENT.NEWSTATCBOXV2
MOVE TRUE             TO  INCCOMMENT.NEWPRIOCBOXV1
MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV2
MOVE FALSE            TO  INCCOMMENT.NEWPRIOCBOXV3
/*
V-SUBERRFOUND := FALSE
/*
END-SUBROUTINE
/* =======================================================================
DEFINE SUBROUTINE FILL-INCDETAILS
/*
V-SUBERRFOUND := TRUE
/*
MOVE CAPL_SELECTED.CAPLVA5 TO CCG-INCSELECTEDID
/*
IF CCG-INCSELECTEDID = " "
  COMPRESS V-TOPMESSAGE (1) INTO CAP.MESSAGE
  ESCAPE ROUTINE
END-IF
/*
FIND INCV INCV.INC-ID = CCG-INCSELECTEDID
  IF NO
    COMPRESS V-TOPMESSAGE (3) INTO CAP.MESSAGE
    ESCAPE ROUTINE
  END-NOREC
/*
  RESET V-BLOB-B  V-BLOB-A V-URL
* REDUCE ARRAY C3XCIOBJECTS TO 0
  REDUCE ARRAY XCIOBJECTS   TO 0
/*
*   MOVE "Type"                 TO  INCDET.LABEL1
*   MOVE "Description"          TO  INCDET.LABEL2
*   MOVE "Creation Date"        TO  INCDET.LABEL3
*   MOVE "Reporter"             TO  INCDET.LABEL4
*   MOVE "Mail Address"         TO  INCDET.LABEL5
*   MOVE "Incidemt Location"    TO  INCDET.LABEL6
*   MOVE "Street"               TO  INCDET.LABEL7
*   MOVE "Status"               TO  INCDET.LABEL8
*   MOVE "Priority"             TO  INCDET.LABEL9
*   MOVE "Date estimated End"   TO  INCDET.LABEL10
/*
  MOVE INCV.INC-TYPE                       TO  INCDET.VALUE1
  MOVE INCV.INC-DESC                       TO  INCDET.VALUE2
  MOVE SUBSTR(INCV.INC-OPENED,1,16)        TO  INCDET.VALUE3
  COMPRESS  INC-REP-FNAME INC-REP-LNAME  INTO  INCDET.VALUE4
  MOVE INC-REP-MAIL                        TO  INCDET.VALUE5  
  COMPRESS INCV.INC-ZIP  INCV.INC-CITY   INTO  INCDET.VALUE6
  MOVE INCV.INC-STREET                     TO  INCDET.VALUE7
  MOVE INCV.INC-STAT                       TO  INCDET.VALUE8
  MOVE INCV.INC-PRIO                       TO  INCDET.VALUE9
  MOVE SUBSTR(INCV.INC-EST-END,1,10)       TO  INCDET.VALUE10 
/*
/* ------------------- Anzeige Bild
  MOVE INCV.INC-PHOTOBIN(1) TO V-BLOB-B  V-BLOB-A
/*
  IF V-BLOB-A NE ' '
    CALLNAT "MAKEURL" XCIOBJECTS(*) V-BLOB-B V-URL
    EXPAND ARRAY XCIOBJECTS   TO (1:1)
    MOVE V-URL TO INCDET.VALUEPIC1
  END-IF
/* -------------------- Anzeige Map
  COMPRESS INCV.INC-STREET ',' INCV.INC-CITY INTO  OSADRESS
/* 
END-FIND
/*
V-SUBERRFOUND := FALSE
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE SET-GE
/*
MOVE 2 TO CCG-LANG *LANGUAGE
PERFORM SET-LLSTRINGS
IF     CCG-LOGINSTATUS = 1
  FETCH RETURN 'CMINIGLP'
END-IF
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE SET-EN
/*
MOVE 1 TO CCG-LANG *LANGUAGE
PERFORM SET-LLSTRINGS
IF     CCG-LOGINSTATUS = 1
  FETCH RETURN 'CMINIGLP'
END-IF
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE SET-SP
/*
MOVE 4 TO CCG-LANG *LANGUAGE
PERFORM SET-LLSTRINGS
IF     CCG-LOGINSTATUS = 1
  FETCH RETURN 'CMINIGLP'
END-IF
/*
END-SUBROUTINE
/* =======================================================================
DEFINE SUBROUTINE SET-PO
/*
MOVE 11 TO CCG-LANG *LANGUAGE
PERFORM SET-LLSTRINGS
IF     CCG-LOGINSTATUS = 1
  FETCH RETURN 'CMINIGLP'
END-IF
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE SET-LOGIN
/*
MOVE 1 TO CCG-LOGINSTATUS

FETCH RETURN 'CMINIGLP'
/*
END-SUBROUTINE
/* =======================================================================
/* =======================================================================
DEFINE SUBROUTINE SET-LOGOUT
/*
MOVE 0 TO CCG-LOGINSTATUS
PANELVISI_LOGIN        :=  TRUE
/* FETCH RETURN 'CMINIGLP'
/*
END-SUBROUTINE
/* =======================================================================
/* ----------------------------------------------
DEFINE SUBROUTINE SET-LLSTRINGS
/*
DECIDE ON FIRST VALUE OF CCG-LANG
/* ---------------------
  VALUE 2   /* german
/*
    V-TOPMESSAGE (1)            := "Bitte Meldung auswählen"
    V-TOPMESSAGE (2)            := "Bitte Kommentar eingeben"
    V-TOPMESSAGE (3)            := "Meldung nnicht vorhanden"
    V-TOPMESSAGE (4)            := "Kommentar wurde verschickt"
    V-TOPMESSAGE (5)            := " "
    V-TOPMESSAGE (6)            := " "
    V-TOPMESSAGE (7)            := " "
    V-TOPMESSAGE (8)            := "Willkommen "
    V-TOPMESSAGE (9)            := "Entschuldigung, ein Fehler ist aufgetreten"
    V-TOPMESSAGE (10)           := "Funktion noch nicht implementiert"
/*     
    INC.STARTTOWN               := "MeineStadt311"
/*    INC.STARTTOWN               := "PROVI2020"
    INC.STARTORG                := "mobile Dienste"
    BUTTONSTART                 := "Start"
/*
    CAP_DETPANELNAME            := "Meldungsdetails"
    CAP_INCCOMMENTPANELNAME     := "Mein Kommentar"
    CAP_INCLISTPANELNAME        := "Liste der Meldungen"
    CAP_PICPANELNAME            := "Bilder"
/*
    MOVE "Mein Kommentar"         TO  INCCOMMENT.COMLABEL
    MOVE "Kommentar sichern"      TO  INCCOMMENT.SAVEBUTTON
    MOVE "Mein Kommentar"         TO  INCCOMMENT.OPENBUTTON
    MOVE "Zurück zur Liste"       TO  INCCOMMENT.BACK2LBUTTON
    MOVE "Zurück zu den Details"  TO  INCCOMMENT.BACK2DBUTTON
    MOVE "Neue Priorität"         TO  INCCOMMENT.TEXTNEWPRIO
    MOVE "Neuer Status"           TO  INCCOMMENT.TEXTNEWSTATUS
    MOVE "Datum vorausschtliche Behebung" TO INCCOMMENT.ENDDATELABEL
    MOVE "Datum der Erstellung"           TO  INCCOMMENT.INCDATELABEL
    MOVE "Typ"                            TO  INCCOMMENT.INCTYPELABEL
/*
    MOVE "Typ"                   TO  INCDET.LABEL1
    MOVE "Beschreibung"          TO  INCDET.LABEL2
    MOVE "Datum der Erstellung"  TO  INCDET.LABEL3
    MOVE "Reporter"              TO  INCDET.LABEL4
    MOVE "Mail Adresse"          TO  INCDET.LABEL5
    MOVE "Ort der Erstellung"    TO  INCDET.LABEL6
    MOVE "Straße"                TO  INCDET.LABEL7
    MOVE "Status"                TO  INCDET.LABEL8
    MOVE "Priorität"             TO  INCDET.LABEL9
    MOVE "Datum vorausschtliche Behebung"   TO  INCDET.LABEL10
/* ---------------------
  VALUE 4   /* spanish
/*
    V-TOPMESSAGE (1)            := "Selecciona Incidente primero"
    V-TOPMESSAGE (2)            := "Introduzca su comentario"
    V-TOPMESSAGE (3)            := "Incidente no se pudo encontrar"
    V-TOPMESSAGE (4)            := "Ha sido enviada"
    V-TOPMESSAGE (5)            := " "
    V-TOPMESSAGE (6)            := " "
    V-TOPMESSAGE (7)            := " "
    V-TOPMESSAGE (8)            := "bienvenida"
    V-TOPMESSAGE (9)            := "Lo sentimos, ocurrió un error"
    V-TOPMESSAGE (10)           := "Función no implementada"
/*
    INC.STARTTOWN               :=  "MiCiudad311"
    INC.STARTORG                :=  "Servicios móviles"
    BUTTONSTART                 :=  "Comienzo"
/*
    CAP_DETPANELNAME            := "Detalles del incidente"
    CAP_INCCOMMENTPANELNAME     := "Mi comentario"
    CAP_INCLISTPANELNAME        := "Todos los incidentes"
    CAP_PICPANELNAME            := "Fotos"
/*
    MOVE "Mi comentario"           TO  INCCOMMENT.COMLABEL
    MOVE "Añadir comentario"       TO  INCCOMMENT.SAVEBUTTON
    MOVE "Mi comentario"           TO  INCCOMMENT.OPENBUTTON
    MOVE "Volver a la lista"       TO  INCCOMMENT.BACK2LBUTTON
    MOVE "Volver a los detalles"   TO  INCCOMMENT.BACK2DBUTTON
    MOVE "Nueva prioridad"         TO  INCCOMMENT.TEXTNEWPRIO
    MOVE "Nuevo estado"            TO  INCCOMMENT.TEXTNEWSTATUS
    MOVE "Data da Suluição (estimada)" TO INCCOMMENT.ENDDATELABEL
    MOVE "Data de criação"             TO  INCCOMMENT.INCDATELABEL
    MOVE "Tipo"                        TO  INCCOMMENT.INCTYPELABEL
/*
/*
    MOVE "Tipo"                        TO  INCDET.LABEL1
    MOVE "Descripción"                 TO  INCDET.LABEL2
    MOVE "Fecha de creación"           TO  INCDET.LABEL3
    MOVE "Reportero"                   TO  INCDET.LABEL4
    MOVE "Dirección de correo electrónico"         TO  INCDET.LABEL5
    MOVE "Ubicación del incidente"     TO  INCDET.LABEL6
    MOVE "Calle"                       TO  INCDET.LABEL7
    MOVE "Estado"                      TO  INCDET.LABEL8
    MOVE "Prioridad"                   TO  INCDET.LABEL9
    MOVE "Data da Solução (estimada)"  TO  INCDET.LABEL10
/* ---------------------
  VALUE 11   /* portuguese
/*
    V-TOPMESSAGE (1)            := "Selecione Incidente primeiro"
    V-TOPMESSAGE (2)            := "Insira o seu comentário"
    V-TOPMESSAGE (3)            := "Incidente não pôde ser encontrado"
    V-TOPMESSAGE (4)            := "Comentário foi enviado"
    V-TOPMESSAGE (5)            := " "
    V-TOPMESSAGE (6)            := " "
    V-TOPMESSAGE (7)            := " "
    V-TOPMESSAGE (8)            := "bem-vindo"
    V-TOPMESSAGE (9)            := "Desculpe, ocorreu um error"
    V-TOPMESSAGE (10)           := "Função não implementada"
/*
    INC.STARTTOWN               :=  "MinhaCidade311"
    INC.STARTORG                :=  "Serviços móveis"
    BUTTONSTART                 :=  "Começo"
/*
    CAP_DETPANELNAME            := "Detalhes do incidente"
    CAP_INCCOMMENTPANELNAME     := "Meu comentário"
    CAP_INCLISTPANELNAME        := "Lista de Incidentes"
    CAP_PICPANELNAME            := "As fotos"
/*
    MOVE "Meu comentário"              TO  INCCOMMENT.COMLABEL
    MOVE "Guardar comentário"          TO  INCCOMMENT.SAVEBUTTON
    MOVE "Meu comentário"              TO  INCCOMMENT.OPENBUTTON
    MOVE "De volta à lista"            TO  INCCOMMENT.BACK2LBUTTON
    MOVE "Voltar para detalhes"        TO  INCCOMMENT.BACK2DBUTTON
    MOVE "Nova prioridade"             TO  INCCOMMENT.TEXTNEWPRIO
    MOVE "Novo status"                 TO  INCCOMMENT.TEXTNEWSTATUS
    MOVE "Data da Solução (estimada)"  TO  INCCOMMENT.ENDDATELABEL
    MOVE "Data de criação"             TO  INCCOMMENT.INCDATELABEL
    MOVE "Tipo"                        TO  INCCOMMENT.INCTYPELABEL
/*
    MOVE "Tipo"                        TO  INCDET.LABEL1
    MOVE "Descrição"                   TO  INCDET.LABEL2
    MOVE "Data de criação"             TO  INCDET.LABEL3
    MOVE "Repórterr"                   TO  INCDET.LABEL4
    MOVE "Endereço de e-mail"          TO  INCDET.LABEL5
    MOVE "Local do Incidente"          TO  INCDET.LABEL6
    MOVE "Rua"                         TO  INCDET.LABEL7
    MOVE "Status"                      TO  INCDET.LABEL8
    MOVE "Prioridade"                  TO  INCDET.LABEL9
    MOVE "Data da Solução (estimada)"  TO  INCDET.LABEL10
/*Type
/* ---------------------
  NONE      /* english and rest of world
/*
    V-TOPMESSAGE (1)            := "Please select Incident first"
    V-TOPMESSAGE (2)            := "Please insert your comment"
    V-TOPMESSAGE (3)            := "Incident could not be found"
    V-TOPMESSAGE (4)            := "Comment was sended"
    V-TOPMESSAGE (5)            := " "
    V-TOPMESSAGE (6)            := " "
    V-TOPMESSAGE (7)            := " "
    V-TOPMESSAGE (8)            := "Welcome"
    V-TOPMESSAGE (9)            := "Sorry - error occured"
    V-TOPMESSAGE (10)           := "Function not yet implemented "
/*
    INC.STARTTOWN := "MyHomeTown311"
    INC.STARTORG  := "Mobile services"
/*
    BUTTONSTART := "Start"
/*
    CAP_DETPANELNAME            := "Incident Details"
    CAP_INCCOMMENTPANELNAME     := "My Comment"
    CAP_INCLISTPANELNAME        := "Incidents List"
    CAP_PICPANELNAME            := "Pictures" 
/*
/*
    MOVE "My Comment"           TO  INCCOMMENT.COMLABEL
    MOVE "Save"         TO  INCCOMMENT.SAVEBUTTON
    MOVE "My Comment"           TO  INCCOMMENT.OPENBUTTON
    MOVE "Back to List"         TO  INCCOMMENT.BACK2LBUTTON
    MOVE "Back to Details"      TO  INCCOMMENT.BACK2DBUTTON
    MOVE "New Priority"         TO INCCOMMENT.TEXTNEWPRIO
    MOVE "New Status"           TO INCCOMMENT.TEXTNEWSTATUS
    MOVE "Date of Sulution (estimated)" 
                                TO INCCOMMENT.ENDDATELABEL
    MOVE "Creation Date"        TO  INCCOMMENT.INCDATELABEL
    MOVE "Type"                 TO  INCCOMMENT.INCTYPELABEL    
/*
    MOVE "Type"                 TO  INCDET.LABEL1
    MOVE "Description"          TO  INCDET.LABEL2
    MOVE "Creation Date"        TO  INCDET.LABEL3
    MOVE "Reporter"             TO  INCDET.LABEL4
    MOVE "Mail Address"         TO  INCDET.LABEL5
    MOVE "Incidemt Location"    TO  INCDET.LABEL6
    MOVE "Street"               TO  INCDET.LABEL7
    MOVE "Status"               TO  INCDET.LABEL8
    MOVE "Priority"             TO  INCDET.LABEL9
    MOVE "Date estimated End"   TO  INCDET.LABEL10
/*
/* ---------------------
END-DECIDE
/*
END-SUBROUTINE
/*
END
