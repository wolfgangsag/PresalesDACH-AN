* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* TODO Enter your code here
DEFINE DATA 
PARAMETER USING NCCOMM-P
PARAMETER USING NCCOGE-P 
*
LOCAL USING NCDATA-L
*
LOCAL
1 C-RECCNT (I2)
1 T-START-DATE (A10)
1 T-END-DATE   (A10)
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 V-CONTRACTID (P6)  
1 V-SERVICENAME (A20) CONST<'Lesen Buchungsdaten'>
*
END-DEFINE
*
* ------------------------------------------------------------------
ON ERROR
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
* COMPUTE V-CONTRACTID = V-CONTRACTID / 0 /*Error-test
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME'
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
IF P-SELECTION.P-CONTRACT-ID = ' ' OR NOT P-SELECTION.P-CONTRACT-ID  IS  (P6)
  MOVE 9921 TO MSG-GROUP-PARA.MSG-NR
ELSE
 COMPUTE V-CONTRACTID = VAL(P-SELECTION.P-CONTRACT-ID)
  F1.
 FIND NCCONTRACT WITH NCCONTRACT.CONTRACT-ID = V-CONTRACTID
    IF NO RECORD FOUND
      MOVE 9922 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE BOTTOM (F1.)
    END-NOREC
    MOVE 9803 TO MSG-GROUP-PARA.MSG-NR
    MOVE 1 TO C-RECCNT
    MOVE BY NAME NCCONTRACT TO P-CONTRACT-DATA
*
    MOVE EDITED  NCCONTRACT.PRICE (EM=ZZZZZZZZZ9.99) TO
      P-CONTRACT-DATA.PRICE
    MOVE EDITED  NCCONTRACT.DATE-CANCELLATION (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-CANCELLATION
    MOVE EDITED  NCCONTRACT.DATE-RESERVATION (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-RESERVATION
    MOVE EDITED  NCCONTRACT.DATE-BOOKING     (EM=9999'-'99'-'99) TO
      P-CONTRACT-DATA.DATE-BOOKING
*
    COMPRESS  P-CONTRACT-DATA.CONTRACT-ID  
      INTO P-CONTRACT-DATA.CONTRACT-ID 
      
    COMPRESS  P-CONTRACT-DATA.PRICE
      INTO P-CONTRACT-DATA.PRICE
    COMPRESS  P-CONTRACT-DATA.DATE-RESERVATION
      INTO P-CONTRACT-DATA.DATE-RESERVATION
    COMPRESS  P-CONTRACT-DATA.DATE-BOOKING 
      INTO  P-CONTRACT-DATA.DATE-BOOKING
    COMPRESS  P-CONTRACT-DATA.ID-CRUISE
      INTO P-CONTRACT-DATA.ID-CRUISE
    COMPRESS  P-CONTRACT-DATA.ID-CUSTOMER
      INTO P-CONTRACT-DATA.ID-CUSTOMER
*
EXAMINE P-CONTRACT-DATA.PRICE FOR ' ' DELETE
*
    F2.
    FIND NCCRUISE CRUISE-ID = NCCONTRACT.ID-CRUISE
      COMPRESS NCCRUISE.START-HARBOR '-' NCCRUISE.DESTINATION-HARBOR
        INTO P-CONTRACT-ADDDATA.CRUISE-ROUTE
*
      MOVE EDITED  NCCRUISE.START-DATE (EM=9999'-'99'-'99)
        TO T-START-DATE
      MOVE EDITED  NCCRUISE.END-DATE   (EM=9999'-'99'-'99)
        TO T-END-DATE
      COMPRESS T-START-DATE '-' T-END-DATE
        INTO P-CONTRACT-ADDDATA.CRUISE-DATE
*
      F3.
      FIND NCYACHT NCYACHT.YACHT-ID = NCCRUISE.ID-YACHT
        COMPRESS NCYACHT.YACHT-NAME INTO
          P-CONTRACT-ADDDATA.YACHT-NAME
        ESCAPE BOTTOM (F3.)
      END-FIND
      ESCAPE BOTTOM (F2.)
    END-FIND
*
    F4.
    FIND NCCUSTOMER PERSON-ID = NCCONTRACT.ID-CUSTOMER
      COMPRESS NCCUSTOMER.SURNAME  ',' NCCUSTOMER.FIRST-NAME-1
        INTO P-CONTRACT-ADDDATA.CUSTOMER-NAME LEAVING NO
      ESCAPE BOTTOM (F4.)
    END-FIND
*
  END-FIND
END-IF
*
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
* ------------------------------------------------------------------
*
INCLUDE GETLOG-I 'V-SERVICENAME' 
*
*
END
