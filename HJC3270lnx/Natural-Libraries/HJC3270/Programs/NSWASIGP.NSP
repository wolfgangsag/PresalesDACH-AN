* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* ------------------------------------------------------------------- *
* Member          NSWASIGP
* Component       EWVDEMO
* Created         April 2004
* Copyright       (c) Copyright Software AG 2004. All rights reserved.
* Remarks         Assign vehicle to employee
*
* ------------------------------------------------------------------- *
*
DEFINE DATA
       GLOBAL USING NSW-GDA
       LOCAL USING NSWEMPLO
       LOCAL USING NSWVEHIC
       LOCAL
1 T-CDCURR         (A3/6)
1 T-DSCURR         (A16/6)
1 M-TITLE          (A50)
1 C-VEHNUMBER      (C)
1 C-KEY            (C)
1 MSG-PAR          (A20)
1 MESSAGE          (N4)
1 INDICATION       (I4)
1 M-MAP-DATA
  2 M-KEY          (A8)
  2 M-TNAME        (A27)
  2 M-OCCUPATION   (A25)
  2 M-DEPT         (A6)
  2 M-MODELYEAR    (N4)
  2 M-COLOR        (A10)
  2 M-INDICATION   (A20)
  2 M-VEHNUMBER    (A15)
  2 M-MODEL        (A20)
1 M-PRG-ERROR      (A8)
1 I                (N2)
1 FILLER           (L)
END-DEFINE
*
INCLUDE NSWDIV2C
*
ON ERROR
  INCLUDE NSWERR1C
  ESCAPE ROUTINE
END-ERROR
*
SET KEY ALL
INCLUDE NSWPFK1C
INCLUDE NSWPFK3C
*
DECIDE ON FIRST VALUE OF *LANGUAGE
  VALUE 1
    MOVE '           Vehicle Assignment'   TO M-TITLE
  VALUE 2
    MOVE '            Fahrzeugzuweisung'    TO M-TITLE
  VALUE 4
    MOVE '         Asignación de Vehículos' TO M-TITLE
  NONE VALUE
    IGNORE
END-DECIDE
*
MOVE (AD=P) TO C-KEY
*
REPEAT
  INPUT WITH TEXT *MESSAGE, MSG-PAR MARK INDICATION USING MAP 'NSWASIG&'
*
    DECIDE ON FIRST VALUE OF *PF-KEY
      VALUE 'ENTR'
        IF FILLER EQ FALSE
          RESET MESSAGE
          PERFORM VALIDATIONS-2
          IF MESSAGE NE 0
             ESCAPE TOP
          END-IF
          PERFORM FILL-MAP-DATA
          ESCAPE TOP
        END-IF
        RESET MESSAGE
        PERFORM VALIDATIONS
        IF MESSAGE NE 0
          ESCAPE TOP
        ELSE
          MOVE 14 TO MESSAGE
          ESCAPE TOP
        END-IF
      VALUE 'PF3'
        IF FILLER
          RESET C-VEHNUMBER M-MAP-DATA
          MOVE (AD=P) TO C-KEY
          RESET INITIAL FILLER
          ESCAPE TOP
        END-IF
        ESCAPE BOTTOM
      VALUE 'PF5'
        RESET MESSAGE
        PERFORM VALIDATIONS
        IF MESSAGE NE 0
          ESCAPE TOP
        END-IF
        PERFORM CONFIRMATION
        MOVE 11 TO MESSAGE
        COMPUTE INDICATION = POS(M-VEHNUMBER)
        ESCAPE TOP
      NONE VALUE
        MOVE 6 TO MESSAGE
        ESCAPE TOP
    END-DECIDE
*
END-REPEAT
*
* -------------------------------------------------------------------
* Ã¶          s  u b  r o u t i n e s                                Ã¶
* -------------------------------------------------------------------
*
* --------------------------------------------------------------
DEFINE SUBROUTINE VALIDATIONS-2
* --------------------------------------------------------------
*
F1. FIND NUMBER NSWVEHIC WITH REG-NUM EQ M-VEHNUMBER
  IF *NUMBER (F1.) EQ 0
    REINPUT USING HELP MARK *M-VEHNUMBER
  ELSE
    FIND NSWVEHIC WITH REG-NUM EQ M-VEHNUMBER
      MOVE NSWVEHIC.MAKE TO M-INDICATION
      MOVE MODEL  TO M-MODEL
      MOVE COLOR  TO M-COLOR
      MOVE YEAR TO M-MODELYEAR
      FIND(1) NSWEMPLO WITH PERSONNEL-ID EQ NSWVEHIC.PERSONNEL-ID
        COMPRESS NAME FIRST-NAME INTO M-TNAME
        MOVE JOB-TITLE TO M-OCCUPATION
        MOVE DEPT TO M-DEPT
      END-FIND
    END-FIND
  END-IF
END-SUBROUTINE
*
* ------------------------------------------------------------
DEFINE SUBROUTINE VALIDATIONS
* ------------------------------------------------------------
F2. FIND NUMBER NSWEMPLO WITH PERSONNEL-ID   EQ M-KEY
  IF *NUMBER(F2.) EQ 0
    REINPUT USING HELP MARK *M-KEY
  ELSE
    FIND NSWEMPLO WITH PERSONNEL-ID EQ M-KEY
      COMPRESS NAME FIRST-NAME INTO M-TNAME
      MOVE DEPT TO M-DEPT
      MOVE JOB-TITLE TO M-OCCUPATION
    END-FIND
  END-IF
END-SUBROUTINE
*
* -----------------------------------------------------------
DEFINE SUBROUTINE CONFIRMATION
* -----------------------------------------------------------
F3. FIND(1) NSWVEHIC WITH REG-NUM  EQ M-VEHNUMBER
  MOVE M-KEY    TO NSWVEHIC.PERSONNEL-ID
  UPDATE (F3.)
  END TRANSACTION
END-FIND
END-SUBROUTINE
*
* -----------------------------------------------------------
DEFINE SUBROUTINE FILL-MAP-DATA
* -----------------------------------------------------------
MOVE (AD=P) TO C-VEHNUMBER
RESET C-KEY
MOVE TRUE TO FILLER
F4. FIND(1) NSWVEHIC WITH REG-NUM EQ M-VEHNUMBER
  FIND(1) NSWEMPLO WITH PERSONNEL-ID = NSWVEHIC.PERSONNEL-ID
    MOVE PERSONNEL-ID TO M-KEY
    COMPRESS NAME FIRST-NAME TO M-TNAME
    MOVE JOB-TITLE TO M-OCCUPATION
    MOVE DEPT TO M-DEPT
  END-FIND
END-FIND
END-SUBROUTINE
*
END
