* >Natural Source Header 000000
* :Mode S
* :CP windows-1252
* :LineIncrement 10
* <Natural Source Header
* ---------------------------------------------------------------------*
* Member          NSWVEHCP
* Component       EWVDEMO
* Created         April 2004
* Copyright       (c) Copyright Software AG 2004. All rights reserved.
* Remarks         View vehicles
*
* ---------------------------------------------------------------------*
*
DEFINE DATA
       GLOBAL USING NSW-GDA
       LOCAL USING NSWVEHIC
       LOCAL USING NSWEMPLO
       LOCAL
1 T-CDCURR          (A3/6)
1 T-DSCURR          (A16/6)
1 M-TITLE           (A50)
1 C-VEHNUMBER       (C)
1 MSG-PAR           (A20)
1 MESSAGE           (N4)
1 INDICATION        (I4)
1 M-MAP-DATA
  2 M-KEY           (A8)
  2 M-TNAME         (A30)
  2 M-DAY           (N2)
  2 M-MONTH         (N2)
  2 M-YEAR          (N4)
  2 M-MODELYEAR     (N4)
  2 M-VEHCLASS      (A1)
  2 M-COLOR         (A20)
  2 M-CURRENCY      (A3)
  2 M-DSCURR        (A16)
  2 M-MAINT-COST    (N6)
  2 M-INDICATION    (A20)
  2 M-VEHNUMBER     (A15)
  2 M-MODEL         (A20)
1 M-PRG-ERROR       (A8)
1 I                 (N2)
1 FILLER            (L)
1 M-ACQDATE         (N8)
1 REDEFINE M-ACQDATE
    2 #M-YEAR       (N4)
    2 #M-MONTH      (N2)
    2 #M-DAY        (N2)
END-DEFINE
*
ON ERROR
   INCLUDE NSWERR1C
   ESCAPE ROUTINE
END-ERROR
*
INCLUDE NSWDIV2C
*
SET KEY ALL
INCLUDE NSWPFK1C
*
DECIDE ON FIRST VALUE OF *LANGUAGE
  VALUE 1
    MOVE '               View Vehicles  '  TO M-TITLE
  VALUE 2
    MOVE '            Fahrzeuge anzeigen'  TO M-TITLE
  VALUE 4
    MOVE '          Consulta de Veh?culos' TO M-TITLE
  NONE VALUE
    IGNORE
END-DECIDE
*
R1. REPEAT
*
  INPUT WITH TEXT *MESSAGE, MSG-PAR MARK INDICATION USING MAP 'NSWVEHC&'
*
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'ENTR'
      RESET MESSAGE
      PERFORM VALIDATIONS-2
      IF MESSAGE NE 0
        ESCAPE TOP
      END-IF
      PERFORM FILL-MAP-DATA
    VALUE 'PF3'
      IF FILLER
        RESET C-VEHNUMBER M-MAP-DATA
        RESET INITIAL FILLER
        ESCAPE TOP
      END-IF
      ESCAPE BOTTOM
    NONE VALUE
      MOVE 6 TO MESSAGE
      ESCAPE TOP
  END-DECIDE
*
END-REPEAT
* ------------------------------------------------
DEFINE SUBROUTINE VALIDATIONS-2
* ------------------------------------------------
F1. FIND NUMBER NSWVEHIC WITH REG-NUM EQ M-VEHNUMBER
  IF *NUMBER (F1.) EQ 0
    REINPUT USING HELP MARK *M-VEHNUMBER
  END-IF
END-SUBROUTINE
*
* --------------------------------------------------
DEFINE SUBROUTINE FILL-MAP-DATA
* --------------------------------------------------
MOVE (AD=P) TO C-VEHNUMBER
MOVE TRUE TO FILLER
F4. FIND(1) NSWVEHIC WITH REG-NUM EQ M-VEHNUMBER
  MOVE NSWVEHIC.REG-NUM TO M-VEHNUMBER
  MOVE NSWVEHIC.MAKE    TO M-INDICATION
  MOVE NSWVEHIC.YEAR    TO M-MODELYEAR
  MOVE NSWVEHIC.COLOR   TO M-COLOR
  MOVE NSWVEHIC.MODEL   TO M-MODEL
  MOVE NSWVEHIC.PERSONNEL-ID TO M-KEY
  F5. FIND NSWEMPLO WITH PERSONNEL-ID EQ NSWVEHIC.PERSONNEL-ID
    COMPRESS NAME FIRST-NAME INTO M-TNAME
  END-FIND
  MOVE NSWVEHIC.CLASS    TO M-VEHCLASS
  MOVE NSWVEHIC.DATE-ACQ TO M-ACQDATE
  MOVE #M-DAY      TO M-DAY
  MOVE #M-MONTH    TO M-MONTH
  MOVE #M-YEAR     TO M-YEAR
  MOVE NSWVEHIC.CURR-CODE TO  M-CURRENCY
  FOR I EQ 1 TO 6
    IF M-CURRENCY EQ T-CDCURR(I)
      MOVE T-DSCURR(I) TO M-DSCURR
      ESCAPE BOTTOM
    END-IF
  END-FOR
  MOVE NSWVEHIC.MAINT-COST(1) TO M-MAINT-COST
END-FIND
END-SUBROUTINE
*
END
