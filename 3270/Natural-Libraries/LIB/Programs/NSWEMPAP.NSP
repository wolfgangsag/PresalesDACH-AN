* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* ---------------------------------------------------------------------*
* Member          NSWEMPAP
* Component       EWVDEMO
* Created         April 2004
* Copyright       (c) Copyright Software AG 2004. All rights reserved.
* Remarks         Add new employee
*
* ---------------------------------------------------------------------*
*
DEFINE DATA
       GLOBAL USING NSW-GDA
       LOCAL  USING NSWEMPLO
       LOCAL
1 M-TITLE           (A50)
1 MSG-PAR           (A20)
1 MESSAGE           (N4)
1 INDICATION        (I4)
1 M-MAP-DATA
  2 M-KEY           (A8)
  2 M-CDPOSTAL      (A10)
  2 M-SURNAME       (A20)
  2 M-NAME          (A20)
  2 M-CDMARIT       (A1)
  2 M-DSMARIT       (A15)
  2 M-DATE-OF-BIRTH (N8)
  2 REDEFINE M-DATE-OF-BIRTH
    3 M-DAY         (N2)
    3 M-MONTH       (N2)
    3 M-YEAR        (N4)
    3 REDEFINE M-YEAR
      4 M-YEAR-NO   (N2)
      4 M-YEAR-NX   (N2)
  2 M-GENDER        (A1)
  2 M-ADDRESS       (A20)
  2 M-CITY          (A20)
  2 M-CDCOUNTRY     (A3)
  2 M-DSCOUNTRY     (A15)
  2 M-DEPT          (A6)
  2 M-OCCUPATION    (A25)
  2 M-AREACODE      (A6)
  2 M-PHONE         (A15)
1 M-PRG-ERROR       (A8)
1 M-BIRTH           (A8)
1 REDEFINE M-BIRTH
  2 #M-YEAR         (N4)
  2 #M-MONTH        (N2)
  2 #M-DAY          (N2)
END-DEFINE
*
ON ERROR
  INCLUDE NSWERR1C
  ESCAPE ROUTINE
END-ERROR
*
SET KEY ALL
INCLUDE NSWPFK1C
INCLUDE NSWPFK3C
*
DECIDE ON FIRST VALUE OF *LANGUAGE
  VALUE 1
    MOVE '                Add Employees'        TO M-TITLE
  VALUE 2
    MOVE '        Mitarbeiterdaten hinzuf}gen'  TO M-TITLE
  VALUE 4
    MOVE '             Alta de Empleados'       TO M-TITLE
  NONE VALUE
    IGNORE
END-DECIDE
*
REPEAT
*
  INPUT WITH TEXT *MESSAGE, MSG-PAR MARK INDICATION USING MAP 'NSWEMPA&'
*
    DECIDE ON FIRST VALUE OF *PF-KEY
      VALUE 'ENTR'
        RESET MESSAGE
        PERFORM VALIDATIONS
        IF MESSAGE NE 0
          ESCAPE TOP
        ELSE
          MOVE 14 TO MESSAGE
          ESCAPE TOP
        END-IF
      VALUE 'PF3'
        ESCAPE BOTTOM
      VALUE 'PF5'
        RESET MESSAGE
        PERFORM VALIDATIONS
        IF MESSAGE NE 0
          ESCAPE TOP
        END-IF
        PERFORM CONFIRMATION
        RESET M-MAP-DATA
        MOVE 11 TO MESSAGE
        ESCAPE TOP
      NONE VALUE
        MOVE 6 TO MESSAGE
        ESCAPE TOP
    END-DECIDE
END-REPEAT
* ---------------------------------------------------
DEFINE SUBROUTINE VALIDATIONS
* ----------------------------------------------------
*
IF M-KEY EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-KEY)
  ESCAPE ROUTINE
END-IF
*
IF M-KEY NE ' '
  F1. FIND NUMBER NSWEMPLO WITH PERSONNEL-ID EQ M-KEY
    IF *NUMBER(F1.) GE 1
      DECIDE ON FIRST VALUE OF *LANGUAGE
       VALUE 1
        MOVE 'Key'       TO MSG-PAR
       VALUE 2
        MOVE 'Schl}ssel' TO MSG-PAR
       VALUE 4
        MOVE 'C?digo'    TO MSG-PAR
       NONE VALUE
        IGNORE
      END-DECIDE
      MOVE 10 TO MESSAGE
      COMPUTE INDICATION = POS(M-KEY)
      ESCAPE ROUTINE
    END-IF
END-IF
*
IF M-SURNAME EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-SURNAME)
  ESCAPE ROUTINE
END-IF
*
IF M-NAME EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-NAME)
  ESCAPE ROUTINE
END-IF
*
IF M-CDMARIT EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDMARIT)
  ESCAPE ROUTINE
END-IF
*
IF M-CDMARIT NE ' '
  DECIDE ON FIRST VALUE OF *LANGUAGE
    VALUE 1
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'S'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'marital status' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    VALUE 2
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'S'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'Familienstand' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    VALUE 4
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'C'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'Estado civil' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    NONE VALUE
      IGNORE
  END-DECIDE
END-IF
*
IF M-GENDER EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-GENDER)
  ESCAPE ROUTINE
END-IF
*
IF M-GENDER NE ' '
  IF M-GENDER NE 'F' AND M-GENDER NE 'M'
       MOVE 2 TO MESSAGE
       DECIDE ON FIRST VALUE OF *LANGUAGE
        VALUE 1
         MOVE 'gender'     TO MSG-PAR
        VALUE 2
         MOVE 'Geschlecht' TO MSG-PAR
        VALUE 4
         MOVE 'Sexo'       TO MSG-PAR
        NONE VALUE
         IGNORE
       END-DECIDE
       COMPUTE INDICATION = POS(M-GENDER)
       ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-DATE-OF-BIRTH NE MASK(DDMMYYYY)
  MOVE 9 TO MESSAGE
  COMPUTE INDICATION = POS(M-DAY)
  ESCAPE ROUTINE
END-IF
*
RESET M-YEAR-NO
IF M-DATE-OF-BIRTH NE 0
  IF M-YEAR GT 50
    MOVE 19 TO M-YEAR-NO
  ELSE
    MOVE 20 TO M-YEAR-NO
  END-IF
  IF M-DATE-OF-BIRTH NE MASK(DDMMYYYY)
    MOVE 9 TO MESSAGE
    COMPUTE INDICATION = POS(M-DAY)
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-CDCOUNTRY NE ' '
  IF M-CDCOUNTRY NE 'D' AND M-CDCOUNTRY NE 'SPA' AND M-CDCOUNTRY NE 'UK'
     AND M-CDCOUNTRY NE 'FR' AND M-CDCOUNTRY NE 'USA'
       MOVE 2 TO MESSAGE
       DECIDE ON FIRST VALUE OF *LANGUAGE
        VALUE 1
         MOVE 'country' TO MSG-PAR
        VALUE 2
         MOVE 'Staat'   TO MSG-PAR
        VALUE 4
         MOVE 'Pa?s'    TO MSG-PAR
        NONE VALUE
         IGNORE
       END-DECIDE
       COMPUTE INDICATION = POS(M-CDCOUNTRY)
       ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-ADDRESS EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-ADDRESS)
  ESCAPE ROUTINE
END-IF
*
IF M-CITY EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CITY)
  ESCAPE ROUTINE
END-IF
*
IF M-CDCOUNTRY EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDCOUNTRY)
  ESCAPE ROUTINE
END-IF
*
IF M-CDPOSTAL EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDPOSTAL)
  ESCAPE ROUTINE
END-IF
*
IF M-DEPT EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-DEPT)
  ESCAPE ROUTINE
END-IF
*
IF M-OCCUPATION EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-OCCUPATION)
  ESCAPE ROUTINE
END-IF
*
END-SUBROUTINE
*
* ----------------------------------------------------------
DEFINE SUBROUTINE CONFIRMATION
* ----------------------------------------------------------
MOVE M-KEY TO NSWEMPLO.PERSONNEL-ID
MOVE M-SURNAME TO NSWEMPLO.NAME
MOVE M-NAME TO NSWEMPLO.FIRST-NAME
MOVE M-YEAR    TO #M-YEAR
MOVE M-MONTH    TO #M-MONTH
MOVE M-DAY    TO #M-DAY
MOVE EDITED M-BIRTH TO NSWEMPLO.BIRTH (EM=YYYYMMDD)
MOVE M-GENDER TO NSWEMPLO.SEX
MOVE M-ADDRESS  TO NSWEMPLO.ADDRESS-LINE(1)
MOVE M-CDMARIT TO NSWEMPLO.MAR-STAT
MOVE M-CITY TO NSWEMPLO.CITY
MOVE M-CDCOUNTRY TO NSWEMPLO.COUNTRY
MOVE M-DEPT TO NSWEMPLO.DEPT
MOVE M-CDPOSTAL TO NSWEMPLO.POST-CODE
MOVE M-OCCUPATION TO NSWEMPLO.JOB-TITLE
MOVE M-PHONE TO PHONE
MOVE M-AREACODE TO AREA-CODE
STORE NSWEMPLO
END TRANSACTION
END-SUBROUTINE
*
END
