* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* TODO Enter your code here
DEFINE DATA 
PARAMETER USING NCCOMM-P
PARAMETER USING NCCONW-P
*
LOCAL USING NCDATA-L
LOCAL
1 UP-INPUT-OK (L)
1 ID-CUSTOMER-IN-N (N8)
1 ID-CRUISE-IN-N (N8)
1 CHECK-DATE (A8)
1 REDEFINE CHECK-DATE
  2 CHECK-YEAR-N (N4)
  2 CHECK-MON-N  (A2)
  2 CHECK-DAY-N  (A2)
1 REDEFINE CHECK-DATE
  2 CHECK-DATE-N (N8)
*
1 C-NEWCONTRACT (N8)
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 V-SERVICENAME (A25) CONST<'Anlegen Buchungsdaten'>
*
END-DEFINE
* ------------------------------------------------------------------
ON ERROR
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  BACKOUT TRANSACTION
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
MOVE ALL '-' TO V-WORKFIELD
INCLUDE STALOG-I 'V-SERVICENAME'
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
DECIDE FOR FIRST CONDITION
  WHEN WEEK-COUNT-IN         = ' ' MOVE 9901 TO MSG-GROUP-PARA.MSG-NR
  WHEN DATE-RESERVATION-IN   = ' ' MOVE 9902 TO MSG-GROUP-PARA.MSG-NR
*  WHEN DATE-BOOKING-IN       = ' ' MOVE 9903 TO MSG-GROUP-PARA.MSG-NR
  WHEN ID-CUSTOMER-IN        = ' ' MOVE 9904 TO MSG-GROUP-PARA.MSG-NR
  WHEN ID-CUSTOMER-IN        = ' ' MOVE 9905 TO MSG-GROUP-PARA.MSG-NR
*
  WHEN NONE
    FOR1.
    FOR C-NEWCONTRACT 1 TO 10000
      F1.
      FIND NCCONTRACT WITH NCCONTRACT.CONTRACT-ID = C-NEWCONTRACT
        IF NO RECORD FOUND
          MOVE C-NEWCONTRACT TO NCCONTRACT.CONTRACT-ID
          PERFORM HANDLE-INPUT-DATA

          IF MSG-GROUP-PARA.MSG-NR = 9800
            STORE NCCONTRACT
            END TRANSACTION
            MOVE C-NEWCONTRACT TO P-NEW-CONTRACTID 

          ELSE
            BACKOUT TRANSACTION
          END-IF
          ESCAPE BOTTOM (FOR1.)
        END-NOREC
      END-FIND
    END-FOR
END-DECIDE
*
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
DEFINE SUBROUTINE HANDLE-INPUT-DATA
*
MOVE 9800 TO MSG-GROUP-PARA.MSG-NR
*
DECIDE FOR EVERY CONDITION
*
  WHEN WEEK-COUNT-IN NE '1' AND WEEK-COUNT-IN NE '2'
      AND  WEEK-COUNT-IN NE '3'
    MOVE 9915 TO MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
*
  WHEN NOT ID-CUSTOMER-IN IS (N8)
    MOVE 9919 TO MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
  WHEN ID-CUSTOMER-IN IS (N8)  
    COMPUTE ID-CUSTOMER-IN-N  = VAL(ID-CUSTOMER-IN)
    FIND NCCUSTOMER PERSON-ID = ID-CUSTOMER-IN-N
      IF NO RECORDS FOUND
        MOVE 9918 TO MSG-GROUP-PARA.MSG-NR
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
    MOVE ID-CUSTOMER-IN-N TO NCCONTRACT.ID-CUSTOMER
*
  WHEN NOT ID-CRUISE-IN IS (N8)
    MOVE 9917 TO MSG-GROUP-PARA.MSG-NR
    ESCAPE ROUTINE
  WHEN ID-CRUISE-IN IS (N8)  
    COMPUTE ID-CRUISE-IN-N  = VAL(ID-CRUISE-IN)
    FIND NCCRUISE CRUISE-ID = ID-CRUISE-IN-N
      IF NO RECORDS FOUND
        MOVE 9916 TO MSG-GROUP-PARA.MSG-NR
        ESCAPE ROUTINE
      END-NOREC
      MOVE ID-CRUISE-IN-N TO NCCONTRACT.ID-CRUISE
    END-FIND
*
  WHEN DATE-RESERVATION-IN NE ' '
    IF DATE-RESERVATION-IN NE MASK (YYYYMMDD)
      MOVE 9914 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE DATE-RESERVATION-IN TO CHECK-DATE
    IF CHECK-YEAR-N LT 2015 OR CHECK-YEAR-N GT 2020
      MOVE 9913 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE CHECK-DATE-N TO NCCONTRACT.DATE-RESERVATION
*
  WHEN DATE-BOOKING-IN NE ' '
    IF DATE-BOOKING-IN NE MASK (YYYYMMDD)
      MOVE 9912 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE DATE-BOOKING-IN TO CHECK-DATE
    IF CHECK-YEAR-N LT 2015 OR CHECK-YEAR-N GT 2020
      MOVE 9911 TO MSG-GROUP-PARA.MSG-NR
      ESCAPE ROUTINE
    END-IF
    MOVE CHECK-DATE-N TO NCCONTRACT.DATE-BOOKING 
*
  WHEN WEEK-COUNT-IN NE  ' '
    DECIDE ON FIRST VALUE OF WEEK-COUNT-IN
      VALUE '1'
        MOVE NCCRUISE.PRICE-1W TO NCCONTRACT.PRICE
      VALUE '2'
        MOVE NCCRUISE.PRICE-2W TO NCCONTRACT.PRICE
      VALUE '3'
        MOVE NCCRUISE.PRICE-3W TO NCCONTRACT.PRICE
      NONE IGNORE
    END-DECIDE
*
  WHEN NONE IGNORE
END-DECIDE
*
END-SUBROUTINE
* ------------------------------------------------------------------
*
INCLUDE NEWLOG-I 'V-SERVICENAME'  
*
END
