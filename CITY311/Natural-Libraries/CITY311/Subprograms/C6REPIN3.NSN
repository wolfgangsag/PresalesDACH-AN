* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
**********************************************************************
*
/**   SERVER PROGRAM CSREPINC.
/**          PATH: CSREPINC.IDL
/**   IDL LIBRARY: DEMO311
/**   IDL PROGRAM: CSREPINC
/**
/**   GENERATED BY SOFTWARE AG,
/**     NATURAL WRAPPER 9.5.1.0.282
/**     NATURAL 08.03.01, SYSIDL 6.3.13.0
/**   ON 2014-10-06 12:56:05
/**
/** <h1>Subprogram C6REPINC.</h1>
/** <p>Subprogram, welches von CAMENUP.</p>
/** <p>New important statements:</p>
/**     <ul type="square">
/**         <li>Erstellt die Incidents in der DB</li>
/**     </ul>
/** <p>Historie Änderungen</p>
/**     <ul type="square">
/**         <li>November 2017: Ergänzung von 3 CALLNAT für Demo Polizei</li>
/**     </ul>
/** :author rfa
**********************************************************************
DEFINE DATA
PARAMETER
1 LANGUAGEKEY      (A) DYNAMIC         /* :IN
1 INCIDENTDATA                         /** :IN
  2 INCIDENTTYPE       (A) DYNAMIC       /** :IN
  2 DESCRIPTION        (A) DYNAMIC       /** :IN
  2 COMMENTS           (1:*)              /** :IN
    3 COMMDESCRIPTION    (A) DYNAMIC       /** :IN
    3 COMMDATE           (A) DYNAMIC       /** :IN
    3 COMMESTIMATEDENDDATE (A) DYNAMIC     /** :IN
    3 COMMAUTHOR         (A) DYNAMIC       /** :IN
    3 COMMPRIORITY       (A) DYNAMIC       /** :IN
    3 COMMSTATUS         (A) DYNAMIC       /** :IN
  2 DATEOPENED         (A) DYNAMIC       /** :IN
  2 PHOTOS             (A/1:*) DYNAMIC   /** :IN
  2 AUDIOS             (A/1:*) DYNAMIC   /** :IN
  2 ADDRESS                              /** :IN
    3 POSTAL                               /** :IN
      4 STREET             (A) DYNAMIC       /** :IN
      4 CITY               (A) DYNAMIC       /** :IN
      4 ZIPCODE            (A) DYNAMIC       /** :IN
    3 GPS                                  /** :IN
      4 LATITUDE           (F4)              /** :IN
      4 LONGITUDE          (F4)              /** :IN
  2 PRIORITY           (A) DYNAMIC       /** :IN
  2 STATUS             (A) DYNAMIC       /** :IN
  2 REPORTER                             /** :IN
    3 FIRSTNAME          (A) DYNAMIC       /** :IN
    3 LASTNAME           (A) DYNAMIC       /** :IN
    3 CONTACTMAIL        (A) DYNAMIC       /** :IN
    3 PHONENUMBER        (A) DYNAMIC       /** :IN
    3 CALLBACKOPTION     (L)               /** :IN
  2 RELATEDNEWS        (A) DYNAMIC   /** :IN
1 INC-ID           (A) DYNAMIC         /** :OUT
1 ERRORSTATUS      (A) DYNAMIC         /** :OUT
1 ERRORLINE        (A) DYNAMIC         /** :OUT
1 ERRORPRGM        (A) DYNAMIC         /** :OUT
LOCAL USING MSGVIEWS
LOCAL
01 #V-MSG     (A20)
01 REDEFINE #V-MSG
  02 #V-MSGTYPE (A3)
  02 #V-MSGNR   (A17)
/*
01 #MSG-NUM   (N7)
/*
01 #V-TMPSTAMP-B   (B8)
01 #V-TMPSTAMP-P   (P19)
/*
01 #UIG-CLAM-NR    (A10)
/*
01 L-DUMMYFIELD (A1)
01 #LOCLANG (A1)
/*
/* FIELDS FOR CODE CONVERSION
/*
1 #C-PICCNT (N3)
1 #C-AUDIOCNT (N3)
1 #CC-PARM-FUNCTION              (A2)
1 #CC-PARM-RC                    (I4)
1 #CC-PARM-ERRTXT                (A72)
1 #CC-PARM-A                     (A) DYNAMIC
1 #CC-PARM-B                     (B) DYNAMIC
1 #CC-PARM-RFC                   (B1)
/*
1 #C-HISDESCCNT (N3)
1 #C-HISDESCCNT2 (N3)
/*
1 DIFOAKZ   (A19)   /* RFA FÜR POLIZEIDEMO
1 INPOLAKZ  (A17)   /* RFA FÜR POLIZEIDEMO
1 PAYMENT   (N12)   /* WOHE FÜR POLIZEIDEMO
1 FOUNDFAHN (L)     /* WOHE FÜR POLIZEIDEMO
1 FAHNADRESS (A100) /* WOHE FÜR POLIZEIDEMO
/*
END-DEFINE
/*
MOVE "P" TO #LOCLANG
/*
ON ERROR
  BACKOUT TRANSACTION
  MOVE *ERROR-NR TO ERRORSTATUS
  MOVE *ERROR-LINE TO ERRORLINE
  MOVE *PROGRAM TO ERRORPRGM
  ESCAPE ROUTINE
END-ERROR
/*
CALLNAT 'IN-CHADA' L-DUMMYFIELD /* for avoiding timout error
/*
/* ================================================================
/* GENERATE UNIQUE ID AND STORE MESSAGE IN MESSAGE FILE
/*
MOVE *TIMESTMP TO  #V-TMPSTAMP-B
CALLNAT 'USR1009N' #V-TMPSTAMP-B #V-TMPSTAMP-P
MOVE     RIGHT #V-TMPSTAMP-P TO #V-MSGNR
EXAMINE  #V-MSGNR FOR ' ' REPLACE WITH '0'
/*
/* NOW STORE MESSAGE TYPE INC = INCIDENT
/*
MOVE 'INC'      TO #V-MSGTYPE
MOVE #V-MSGTYPE TO D311MSG.F-MSGTYPE
MOVE #V-MSGNR   TO D311MSG.F-MSGNR
S1. STORE D311MSG
/* ================================================================
/* TRANSFER PARA TO VIEW AND STORE INCIDENT
/*
MOVE #V-MSG                          TO D311INC.INC-ID
/*
* DECIDE ON FIRST VALUE OF #LOCLANG
* VALUE "P"    /* PORT.
*     MOVE "MEDIA"        TO  D311INC.INC-PRIO
*     MOVE "NOVO"         TO  D311INC.INC-STAT
* VALUE "G"    /* GERMAN
*     MOVE "MITTEL"        TO  D311INC.INC-PRIO
*     MOVE "NEU"           TO  D311INC.INC-STAT
* NONE
* END-DECIDE
/*
IF INCIDENTTYPE NE ' '
  MOVE  INCIDENTTYPE            TO D311INC.INC-TYPE
END-IF
* /*
IF DESCRIPTION NE ' '
  MOVE  DESCRIPTION      TO D311INC.INC-DESC
/* RFA - EXIT FÜR DEMO PPMFR - DIFO
*     IF D311INC.INC-TYPE = "Small Traffic Accident"
*       FOUNDFAHN :=  FALSE
*       COMPRESS #V-MSGNR INTO INPOLAKZ LEAVING NO
*       COMPRESS ZIPCODE CITY STREET INTO FAHNADRESS WITH DELIMITER ','
*       CALLNAT 'C6PFAHNN' INPOLAKZ   D311INC.INC-DESC FOUNDFAHN FAHNADRESS
*       IF FOUNDFAHN = FALSE
*           CALLNAT 'C6PAYMTN' D311INC.INC-DESC
*       END-IF
*     END-IF
/* END  EXIT
END-IF
/*
/* handle comment
/*
/* #C-HISDESCCNT := *UBOUND(INCIDENTDATA.COMMDESCRIPTION)
/*
FOR #C-HISDESCCNT2 1 TO *UBOUND(INCIDENTDATA.COMMDESCRIPTION)
  IF INCIDENTDATA.COMMDESCRIPTION(#C-HISDESCCNT2) = ' '
    ESCAPE TOP
  END-IF

  COMPUTE #C-HISDESCCNT = #C-HISDESCCNT + 1
  COMPRESS INCIDENTDATA.COMMDATE(#C-HISDESCCNT)
    TO HIS-DAT(#C-HISDESCCNT)
  COMPRESS INCIDENTDATA.COMMAUTHOR(#C-HISDESCCNT)
    TO INCIDENTDATA.COMMAUTHOR(#C-HISDESCCNT)
  COMPRESS INCIDENTDATA.COMMPRIORITY(#C-HISDESCCNT)
    TO HIS-PRIO(#C-HISDESCCNT)
  COMPRESS INCIDENTDATA.COMMESTIMATEDENDDATE(#C-HISDESCCNT)
    TO INCIDENTDATA.COMMESTIMATEDENDDATE(#C-HISDESCCNT)
  COMPRESS INCIDENTDATA.COMMSTATUS(#C-HISDESCCNT)
    TO HIS-STAT(#C-HISDESCCNT)
  COMPRESS INCIDENTDATA.COMMDESCRIPTION(#C-HISDESCCNT)
    TO HIS-DESC(#C-HISDESCCNT)
END-FOR

/* MOVE DATEOPENED TO  D311INC.INC-OPENED /* ignore incoming value
COMPRESS *DAT4I '&' *TIME          INTO D311INC.INC-OPENED  LEAVING NO
EXAMINE D311INC.INC-OPENED FOR '.' REPLACE WITH ' '
EXAMINE D311INC.INC-OPENED FOR '&' REPLACE WITH ' '
MOVE SUBSTR(D311INC.INC-OPENED,1,19)   TO D311INC.INC-OPENED
/*
/* BILDER MÜSSEN EVTL NOCH BASE64 CODIERT WERDEN
/*
FOR #C-PICCNT 1 TO *UBOUND(PHOTOS)
  IF PHOTOS(#C-PICCNT) NE ' '
    MOVE 'AB' TO #CC-PARM-FUNCTION
    MOVE PHOTOS(#C-PICCNT) TO #CC-PARM-A
    CALLNAT 'USR4210N'  #CC-PARM-FUNCTION #CC-PARM-RC
      #CC-PARM-ERRTXT #CC-PARM-B #CC-PARM-A #CC-PARM-RFC
    MOVE #CC-PARM-B TO D311INC.INC-PHOTOBIN(#C-PICCNT)
/* RFA - EXIT FÜR DEMO PPMFR - DIFO
*     IF D311INC.INC-TYPE = "Small Traffic Accident"
*     COMPRESS #V-MSGNR '-' #C-PICCNT INTO DIFOAKZ LEAVING NO
*     CALLNAT 'C6PDIFON' DIFOAKZ   D311INC.INC-PHOTOBIN(#C-PICCNT)
*     END-IF
/* END  EXIT
  END-IF
END-FOR
/*
FOR #C-AUDIOCNT 1 TO *UBOUND(AUDIOS)
  IF AUDIOS(#C-AUDIOCNT) NE ' '
    MOVE  AUDIOS(#C-AUDIOCNT)        TO D311INC.INC-AUDIOBIN(#C-AUDIOCNT)
  END-IF
END-FOR
/*
IF STREET NE ' '
  MOVE  STREET          TO D311INC.INC-STREET
END-IF
/*
IF CITY NE ' '
  MOVE  CITY             TO D311INC.INC-CITY
END-IF
/*
IF ZIPCODE NE ' '
  MOVE  ZIPCODE          TO D311INC.INC-ZIP
END-IF
/*
IF LATITUDE NE 0
  MOVE  LATITUDE         TO D311INC.INC-GPSLATI
END-IF
/*
IF LONGITUDE NE 0
  MOVE   LONGITUDE       TO D311INC.INC-GPSLONG
END-IF
/*
IF PRIORITY NE ' '
  MOVE PRIORITY TO D311INC.INC-PRIO
END-IF
/*
IF STATUS NE ' '
  MOVE STATUS TO D311INC.INC-STAT
END-IF
/*
IF FIRSTNAME NE ' '
  MOVE  FIRSTNAME        TO D311INC.INC-REP-FNAME
END-IF
/*
IF LASTNAME NE ' '
  MOVE LASTNAME          TO   D311INC.INC-REP-LNAME
END-IF
/*
IF CONTACTMAIL NE ' '
  MOVE  CONTACTMAIL  TO D311INC.INC-REP-MAIL
END-IF
/*
IF PHONENUMBER NE ' '
  MOVE  PHONENUMBER       TO   D311INC.INC-REP-PHONE
END-IF
/*
IF CALLBACKOPTION NE FALSE
  MOVE  CALLBACKOPTION    TO   D311INC.INC-REP-CALLBACK
ELSE
  MOVE '0' TO   D311INC.INC-REP-CALLBACK
END-IF
/*
IF INCIDENTDATA.RELATEDNEWS NE ' '
  MOVE INCIDENTDATA.RELATEDNEWS TO D311INC.INC-RELATEDNEWS
END-IF
/*
/*  INC HISTORY DATA
/*
/*
MOVE D311INC.INC-OPENED   TO  D311INC.HIS-DAT(1)
MOVE D311INC.INC-PRIO     TO  D311INC.HIS-PRIO(1)
MOVE D311INC.INC-STAT     TO  D311INC.HIS-STAT(1)
MOVE D311INC.INC-DESC     TO  D311INC.HIS-DESC(1)
MOVE 'CITIZEN'            TO  D311INC.HIS-AUTHOR(1)
/*
STORE D311INC
END TRANSACTION
MOVE 0 TO ERRORSTATUS
MOVE *TRIM(D311INC.INC-ID,TRAILING) TO  INC-ID
/*
END
