* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
**********************************************************************
*
/**   Server Program CSGETINC.
/**          Path: CS-GETINCDE.idl
/**   IDL Library: DEMO311
/**   IDL Program: CSGETINCDE
/**
/**   Generated by Software AG,
/**     Natural Wrapper 9.5.1.0.282
/**     Natural 08.03.01, SYSIDL 6.3.13.0
/**   on 2014-10-06 10:58:17
/**
/** <h1>Subprogram C6GETINC.</h1>
/** <p>Subprogram, welches vom RPC gerufen wird.</p>
/** <p>New important statements:</p>
/**     <ul type="square">
/**         <li>Liefert Daten eines Incidents</li>
/**     </ul>
/** <p>Historie Änderungen</p>
/**     <ul type="square">
/**         <li>1.</li>
/**     </ul>
/** :author rfa
**********************************************************************
DEFINE DATA PARAMETER
1 INC-ID-IN          (A) DYNAMIC       /* in /** :in
1 LANGUAGEKEY        (A) DYNAMIC       /* in /** :in
1 INCIDENTDETAILS                      /* out /** :out
  2 INC-ID             (A) DYNAMIC       /** :out
  2 INCIDENTTYPE       (A) DYNAMIC       /** :out
  2 DESCRIPTION        (A) DYNAMIC       /** :out
  2 COMMENTS           (1:*)   /** :out
    3 COMMDESCRIPTION    (A) DYNAMIC       /** :out
    3 COMMDATE           (A) DYNAMIC       /** :out
    3 COMMESTIMATEDENDDATE (A) DYNAMIC     /** :out(AV)
    3 COMMAUTHOR         (A) DYNAMIC       /** :out(AV)
    3 COMMPRIORITY       (A) DYNAMIC       /** :out
    3 COMMSTATUS         (A) DYNAMIC       /** :out
  2 DATEOPENED         (A) DYNAMIC       /** :out
  2 PHOTOS             (A/1:*) DYNAMIC   /** :out
  2 AUDIOS             (A/1:*) DYNAMIC   /** :out
  2 ADDRESS                              /** :out
    3 POSTAL                               /** :out
      4 STREET             (A) DYNAMIC       /** :out
      4 CITY               (A) DYNAMIC       /** :out
      4 ZIPCODE            (A) DYNAMIC       /** :out
    3 GPS                                  /** :out
      4 LATITUDE           (F4)              /** :out
      4 LONGITUDE          (F4)              /** :out
  2 ASSIGNEECONTACT    (A) DYNAMIC       /** :out
  2 PRIORITY           (A) DYNAMIC       /** :out
  2 STATUS             (A) DYNAMIC       /** :out
  2 REPORTER                             /** :out
    3 FIRSTNAME          (A) DYNAMIC       /** :out
    3 LASTNAME           (A) DYNAMIC       /** :out
    3 CONTACTMAIL        (A) DYNAMIC       /** :out
    3 PHONENUMBER        (A) DYNAMIC       /** :out
    3 CALLBACKOPTION     (L)               /** :out
  2 ESTIMATEDENDDATE   (A) DYNAMIC       /** :out
  2 RELATEDNEWS        (A/1:*) DYNAMIC   /** :out
1 ERRORSTATUS        (A) DYNAMIC       /* out /** :out
/*
LOCAL USING MSGVIEWS
LOCAL
1 #V-ZIPLOC   (A10)
1 #V-ZIPLOC-N (N10)
1 L-DUMMYFIELD (A1)
1 #C-COMCNT (N3)
1 #C-PICCNT (N3)
1 #C-HIST (I2)
1 #C-HISDESCCNT (N3)
1 #C-HISDESCCNT2 (N3)
/*
/* FIELDS FOR CODE CONVERSION
/*
1 #CC-PARM-FUNCTION              (A2)
1 #CC-PARM-RC                    (I4)
1 #CC-PARM-ERRTXT                (A72)
1 #CC-PARM-A                     (A) DYNAMIC
1 #CC-PARM-B                     (B) DYNAMIC
1 #CC-PARM-RFC                   (B1)
1 #LOCLANG (A1)
END-DEFINE
/*
ON ERROR
  MOVE *ERROR-NR TO ERRORSTATUS
  ESCAPE ROUTINE
END-ERROR
/*

MOVE LANGUAGEKEY TO #LOCLANG
/*
CALLNAT 'IN-CHADA' L-DUMMYFIELD /* for avoiding timeout error
/*
F1. FIND D311INC INC-ID =  INC-ID-IN
  IF NO RECORDS FOUND
    COMPRESS  10999 INTO ERRORSTATUS
    ESCAPE ROUTINE
  END-NOREC
/*
  EXPAND ARRAY PHOTOS      TO (1:3)
  EXPAND ARRAY RELATEDNEWS TO (1:3)
/*
/* INC COMMENTS
/*
/*
  FOR #C-HISDESCCNT2 1 TO 20
    IF HIS-DAT(#C-HISDESCCNT2) = " " ESCAPE TOP END-IF
    COMPUTE #C-HISDESCCNT = #C-HISDESCCNT + 1
    EXPAND ARRAY INCIDENTDETAILS.COMMENTS TO (1:#C-HISDESCCNT)
COMPRESS HIS-DAT(#C-HISDESCCNT)     TO INCIDENTDETAILS.COMMDATE(#C-HISDESCCNT)
COMPRESS HIS-AUTHOR(#C-HISDESCCNT)  TO INCIDENTDETAILS.COMMAUTHOR(#C-HISDESCCNT)
COMPRESS HIS-PRIO(#C-HISDESCCNT)    TO INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT)
COMPRESS HIS-END(#C-HISDESCCNT)     TO INCIDENTDETAILS.COMMESTIMATEDENDDATE(#C-HISDESCCNT)
COMPRESS HIS-STAT(#C-HISDESCCNT)    TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT)
COMPRESS HIS-DESC(#C-HISDESCCNT)    TO INCIDENTDETAILS.COMMDESCRIPTION(#C-HISDESCCNT)
/*
    IF #LOCLANG = "P"
IF INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) = "low"    MOVE "baixa" TO INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) = "medium" MOVE "media" TO INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) = "hight"  MOVE "alta"  TO INCIDENTDETAILS.COMMPRIORITY(#C-HISDESCCNT) END-IF
/*
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "New"                    MOVE "novo"         TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "new"                    MOVE "novo"         TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "unassigned"             MOVE "não delegado" TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "assigned"               MOVE "delegado"     TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "missing information"    MOVE "faltam informações" TO  INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "in Progress"            MOVE "em andamento" TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "solved"                 MOVE "resolvido"    TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
IF INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) = "completed"              MOVE "completado"   TO INCIDENTDETAILS.COMMSTATUS(#C-HISDESCCNT) END-IF
/*
    END-IF
  END-FOR
/*
  COMPRESS D311INC.INC-PRIO INTO PRIORITY
  COMPRESS D311INC.INC-STAT INTO STATUS
/*
  IF #LOCLANG = "P"
    IF PRIORITY = "low"    MOVE "baixa" TO  PRIORITY END-IF
    IF PRIORITY = "medium" MOVE "media" TO  PRIORITY END-IF
    IF PRIORITY = "hight"  MOVE "alta"  TO  PRIORITY END-IF
/*
IF STATUS = "New"                    MOVE "novo"         TO     STATUS END-IF
IF STATUS = "new"                    MOVE "novo"         TO     STATUS END-IF
IF STATUS = "unassigned"             MOVE "não delegado" TO     STATUS END-IF
IF STATUS = "assigned"               MOVE "delegado"     TO     STATUS END-IF
IF STATUS = "missing information"    MOVE "faltam informações" TO  STATUS END-IF
IF STATUS = "in Progress"            MOVE "em andamento" TO     STATUS END-IF
IF STATUS = "solved"                 MOVE "resolvido"    TO     STATUS END-IF
IF STATUS = "completed"              MOVE "completado"   TO     STATUS END-IF
  END-IF
/*
  MOVE  *TRIM(D311INC.INC-ID)              TO INCIDENTDETAILS.INC-ID
  MOVE  *TRIM(D311INC.INC-TYPE)            TO INCIDENTTYPE
  MOVE  *TRIM(D311INC.INC-DESC)            TO DESCRIPTION
  MOVE  *TRIM(D311INC.INC-OPENED)          TO DATEOPENED
  MOVE  *TRIM(D311INC.INC-EST-END)         TO ESTIMATEDENDDATE
  MOVE  *TRIM(D311INC.INC-REP-FNAME)       TO FIRSTNAME
  MOVE  *TRIM(D311INC.INC-REP-LNAME)       TO LASTNAME
  MOVE  *TRIM(D311INC.INC-REP-MAIL)        TO CONTACTMAIL
  MOVE  *TRIM(D311INC.INC-REP-PHONE)       TO PHONENUMBER
  MOVE  TRUE                               TO CALLBACKOPTION
  MOVE  *TRIM(D311INC.INC-CITY)            TO CITY
  MOVE  *TRIM(D311INC.INC-ZIP)             TO ZIPCODE
  MOVE  *TRIM(D311INC.INC-STREET)          TO STREET
  EXPAND ARRAY RELATEDNEWS TO (1:1)
  MOVE  'some News 1'       TO RELATEDNEWS(1)
IF D311INC.INC-GPSLATI NE ' ' ASSIGN  LATITUDE  = VAL(INC-GPSLATI)  END-IF
IF D311INC.INC-GPSLONG NE ' ' ASSIGN  LONGITUDE = VAL(INC-GPSLONG)  END-IF
/*
/* Bilder müssen evtl noch Base64 Codiert werden
/* Kommen binär an (und werden auf dem Übertragungsweg nicht modifiziert, weil es Binärdaten sind.)
/*                                                           -> Konvertierung durch Middleware
/* Daten werden hier in A Felder überführt.
/*
  FOR #C-PICCNT 1 TO 3
    IF INC-PHOTOBIN(#C-PICCNT) NE ' '
      MOVE 'BA' TO #CC-PARM-FUNCTION /* MOVE 'BA' (Konvertierung von Binär nach Alfa)
      MOVE INC-PHOTOBIN(#C-PICCNT) TO #CC-PARM-B
      CALLNAT 'USR4210N'  #CC-PARM-FUNCTION #CC-PARM-RC
        #CC-PARM-ERRTXT #CC-PARM-B #CC-PARM-A #CC-PARM-RFC
      EXPAND ARRAY PHOTOS TO (1:#C-PICCNT)
      MOVE #CC-PARM-A TO PHOTOS(#C-PICCNT)
    END-IF
  END-FOR
/*
END-FIND
/*
MOVE 0 TO ERRORSTATUS
/*
END
