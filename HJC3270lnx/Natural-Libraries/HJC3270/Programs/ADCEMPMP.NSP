* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* ---------------------------------------------------------------------*
* Member          NSWEMPMP
* Component       EWVDEMO
* Created         April 2004
* Copyright       (c) Copyright Software AG 2004. All rights reserved.
* Remarks         Modify employee
*
* -------------------------------------------------------------------- *
*
DEFINE DATA
       GLOBAL USING NSW-GDA
       LOCAL USING NSWEMPLO
       LOCAL
1 MESSAGE           (N4)
1 INDICATION        (I4)
1 MSG-PAR           (A20)
1 M-TITLE           (A50)
1 T-CDLANG          (A3/6)
1 T-DSCOUNTRY       (A15/6)
1 T-CDMARIT         (A1/4)
1 T-DSMARIT         (A15/4)
1 M-PRG-ERROR       (A8)
*                                        /* variables for the map
1 M-DATE-OF-BIRTH   (N8)
1 REDEFINE M-DATE-OF-BIRTH
  2 M-DAY           (N2)
  2 M-MONTH         (N2)
  2 M-YEAR          (N4)
  2 REDEFINE M-YEAR
    3 M-YEAR-NO     (N2)
    3 M-YEAR-NX     (N2)
1 M-ADDRESS         (A20)
1 M-OCCUPATION      (A25)
1 M-CDCOUNTRY       (A3)
1 M-CDPOSTAL        (A10)
1 M-CITY            (A20)
1 M-KEY             (A8)
1 M-DEPT            (A6)
1 M-DSMARIT         (A15)
1 M-DSCOUNTRY       (A15)
1 M-DSGEND          (A15)
1 M-CDMARIT         (A1)
1 M-SURNAME         (A20)
1 M-NAME            (A20)
1 I                 (N2)
1 M-BIRTH           (A8)
1 REDEFINE M-BIRTH
  2 #M-YEAR         (N4)
  2 #M-MONTH        (N2)
  2 #M-DAY          (N2)
1 M-PHONE           (A15)
1 REDEFINE M-PHONE
  2 #M-PHONE        (I4)
1 M-AREACODE        (A6)
1 M-GENDER          (A1)
END-DEFINE
*
SET KEY ALL
INCLUDE NSWPFK1C
INCLUDE NSWPFK3C
*
ON ERROR
  INCLUDE NSWERR1C
  ESCAPE ROUTINE
END-ERROR
*
INCLUDE NSWEST2C
INCLUDE NSWPAI2C
*
DECIDE ON FIRST VALUE OF *LANGUAGE
  VALUE 1
    MOVE '             Modify Employees'      TO M-TITLE
  VALUE 2
    MOVE '          Mitarbeiterdaten {ndern'  TO M-TITLE
  VALUE 4
    MOVE '        Modificaci?n de Empleados'  TO M-TITLE
  NONE VALUE
    IGNORE
END-DECIDE
*
PERFORM FILL-MAP-DATA
*
R1. REPEAT
*
  INPUT WITH TEXT *MESSAGE, MSG-PAR MARK INDICATION USING MAP 'NSWEMPM&'
*
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM
    VALUE 'PF5'
      RESET MESSAGE
      PERFORM VALIDATIONS
      IF MESSAGE NE 0
        ESCAPE TOP
      END-IF
      PERFORM CONFIRMATION
      MOVE 11 TO MESSAGE
      ESCAPE TOP
    VALUE 'ENTR'
      RESET MESSAGE
      PERFORM VALIDATIONS
      IF MESSAGE NE 0
        ESCAPE TOP
      ELSE
        MOVE 14 TO MESSAGE
        ESCAPE TOP
      END-IF
    NONE VALUE
      MOVE 6 TO MESSAGE
  END-DECIDE
END-REPEAT
*
* -----------------------------------------------
DEFINE SUBROUTINE FILL-MAP-DATA
* -----------------------------------------------

/* FOR I EQ 1 TO 100

/* MOVE I TO PHONE
REPEAT

F2. FIND NSWEMPLO WITH PERSONNEL-ID = PHONE /* EQ G-KEY
  MOVE PERSONNEL-ID   TO M-KEY
  MOVE ADDRESS-LINE(1) TO M-ADDRESS
  MOVE EDITED BIRTH (EM=YYYYMMDD) TO M-BIRTH
  MOVE #M-YEAR    TO M-YEAR
  MOVE #M-MONTH   TO M-MONTH
  MOVE #M-DAY     TO M-DAY
  MOVE SEX        TO M-GENDER
  MOVE CITY       TO M-CITY
  MOVE JOB-TITLE  TO M-OCCUPATION
  MOVE DEPT       TO M-DEPT
  MOVE POST-CODE  TO M-CDPOSTAL
  MOVE NAME       TO M-SURNAME
  MOVE FIRST-NAME TO M-NAME
  MOVE MAR-STAT   TO M-CDMARIT
  MOVE COUNTRY    TO M-CDCOUNTRY
  MOVE PHONE      TO M-PHONE
  MOVE AREA-CODE  TO M-AREACODE
END-FIND

END-REPEAT
/* END-FOR

FOR I EQ 1 TO 5
  IF M-CDCOUNTRY EQ T-CDLANG(I)
    MOVE T-DSCOUNTRY(I) TO M-DSCOUNTRY
    ESCAPE BOTTOM
  END-IF
END-FOR
FOR I EQ 1 TO 4
  IF M-CDMARIT EQ T-CDMARIT(I)
    MOVE T-DSMARIT(I) TO M-DSMARIT
    ESCAPE BOTTOM
  END-IF
END-FOR
END-SUBROUTINE
*
* ----------------------------------------------
DEFINE SUBROUTINE VALIDATIONS
* ----------------------------------------------
*
IF M-SURNAME EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-SURNAME)
  ESCAPE ROUTINE
END-IF
*
IF M-NAME EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-NAME)
  ESCAPE ROUTINE
END-IF
*
IF M-CDMARIT EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDMARIT)
  ESCAPE ROUTINE
END-IF
*
IF M-CDMARIT NE ' '
  DECIDE ON FIRST VALUE OF *LANGUAGE
    VALUE 1
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'S'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'marital status' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    VALUE 2
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'S'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'Familienstand' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    VALUE 4
      IF M-CDMARIT NE 'M' AND M-CDMARIT NE 'C'
         AND M-CDMARIT NE  'D' AND M-CDMARIT NE 'W'
         MOVE 'Estado civil' TO MSG-PAR
         MOVE 2 TO MESSAGE
         COMPUTE INDICATION = POS(M-CDMARIT)
         ESCAPE ROUTINE
      END-IF
    NONE VALUE
      IGNORE
  END-DECIDE
END-IF
*
IF M-GENDER EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-GENDER)
  ESCAPE ROUTINE
END-IF
*
IF M-GENDER NE ' '
  IF M-GENDER NE 'F' AND M-GENDER NE 'M'
       MOVE 2 TO MESSAGE
       DECIDE ON FIRST VALUE OF *LANGUAGE
        VALUE 1
         MOVE 'gender'     TO MSG-PAR
        VALUE 2
         MOVE 'Geschlecht' TO MSG-PAR
        VALUE 4
         MOVE 'Sexo'       TO MSG-PAR
        NONE VALUE
         IGNORE
       END-DECIDE
       COMPUTE INDICATION = POS(M-GENDER)
       ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-DATE-OF-BIRTH NE MASK(DDMMYYYY)
  MOVE 9 TO MESSAGE
  COMPUTE INDICATION = POS(M-DAY)
  ESCAPE ROUTINE
END-IF
*
RESET M-YEAR-NO
IF M-DATE-OF-BIRTH NE 0
  IF M-YEAR GT 50
    MOVE 19 TO M-YEAR-NO
  ELSE
    MOVE 20 TO M-YEAR-NO
  END-IF
  IF M-DATE-OF-BIRTH NE MASK(DDMMYYYY)
    MOVE 9 TO MESSAGE
    COMPUTE INDICATION = POS(M-DAY)
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-CDCOUNTRY NE ' '
  IF M-CDCOUNTRY NE 'D' AND M-CDCOUNTRY NE 'SPA' AND M-CDCOUNTRY NE 'UK'
     AND M-CDCOUNTRY NE 'FR' AND M-CDCOUNTRY NE 'USA'
       MOVE 2 TO MESSAGE
       DECIDE ON FIRST VALUE OF *LANGUAGE
        VALUE 1
         MOVE 'Country' TO MSG-PAR
        VALUE 2
         MOVE 'Staat'   TO MSG-PAR
        VALUE 4
         MOVE 'Pa?s'    TO MSG-PAR
        NONE VALUE
         IGNORE
       END-DECIDE
       COMPUTE INDICATION = POS(M-CDCOUNTRY)
       ESCAPE ROUTINE
  END-IF
END-IF
*
IF M-ADDRESS EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-ADDRESS)
  ESCAPE ROUTINE
END-IF
*
IF M-CITY EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CITY)
  ESCAPE ROUTINE
END-IF
*
IF M-CDCOUNTRY EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDCOUNTRY)
  ESCAPE ROUTINE
END-IF
*
IF M-CDPOSTAL EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-CDPOSTAL)
  ESCAPE ROUTINE
END-IF
*
IF M-DEPT EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-DEPT)
  ESCAPE ROUTINE
END-IF
*
IF M-OCCUPATION EQ ' '
  MOVE 1 TO MESSAGE
  COMPUTE INDICATION = POS(M-OCCUPATION)
  ESCAPE ROUTINE
END-IF
*
END-SUBROUTINE
*
* -------------------------------------------------
DEFINE SUBROUTINE CONFIRMATION
* -------------------------------------------------
F1. FIND NSWEMPLO WITH PERSONNEL-ID EQ G-KEY
  MOVE M-KEY  TO PERSONNEL-ID
  MOVE M-ADDRESS   TO ADDRESS-LINE (1)
  MOVE M-YEAR     TO #M-YEAR
  MOVE M-MONTH     TO #M-MONTH
  MOVE M-DAY     TO #M-DAY
  MOVE EDITED M-BIRTH TO BIRTH (EM=YYYYMMDD)
  MOVE M-CITY  TO CITY
  MOVE M-OCCUPATION TO JOB-TITLE
  MOVE M-DEPT    TO DEPT
  MOVE M-CDPOSTAL TO POST-CODE
  MOVE M-SURNAME  TO NAME
  MOVE M-NAME  TO FIRST-NAME
  MOVE M-CDMARIT  TO MAR-STAT
  MOVE M-CDCOUNTRY  TO COUNTRY
  MOVE M-PHONE TO PHONE
  MOVE M-AREACODE TO AREA-CODE
  MOVE M-GENDER TO SEX
  UPDATE
  END TRANSACTION
END-FIND
END-SUBROUTINE
*
END
